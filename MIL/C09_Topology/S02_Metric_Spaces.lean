import MIL.Common
import Mathlib.Topology.Instances.Real
import Mathlib.Analysis.NormedSpace.BanachSteinhaus

open Set Filter
open Topology Filter

/- TEXT:
.. index:: metric space

.. _metric_spaces:

Metric spaces
--------------

Examples in the previous section focus on sequences of real numbers. In this section we will go up a bit in generality and focus on
metric spaces. A metric space is a type ``X`` equipped with a distance function ``dist : X ‚Üí X ‚Üí ‚Ñù`` which is a generalization of
the function ``fun x y ‚Ü¶ |x - y|`` from the case where ``X = ‚Ñù``.

Introducing such a space is easy and we will check all properties required from the distance function.
BOTH: -/
-- QUOTE:
variable {X : Type*} [MetricSpace X] (a b c : X)

#check (dist a b : ‚Ñù)
#check (dist_nonneg : 0 ‚â§ dist a b)
#check (dist_eq_zero : dist a b = 0 ‚Üî a = b)
#check (dist_comm a b : dist a b = dist b a)
#check (dist_triangle a b c : dist a c ‚â§ dist a b + dist b c)
-- QUOTE.

/- TEXT:
Note we also have variants where the distance can be infinite or where ``dist a b`` can be zero without having ``a = b`` or both.
They are called ``EMetricSpace``, ``PseudoMetricSpace`` and ``PseudoEMetricSpace`` respectively (here "e" stands for "extended").

BOTH: -/
-- Note the next three lines are not quoted, their purpose is to make sure those things don't get renamed while we're looking elsewhere.
#check EMetricSpace
#check PseudoMetricSpace
#check PseudoEMetricSpace

/- TEXT:
Note that our journey from ``‚Ñù`` to metric spaces jumped over the special case of normed spaces that also require linear algebra and
will be explained as part of the calculus chapter.

Convergence and continuity
^^^^^^^^^^^^^^^^^^^^^^^^^^

Using distance functions, we can already define convergent sequences and continuous functions between metric spaces.
They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition in terms of distances.
BOTH: -/
-- QUOTE:
example {u : ‚Ñï ‚Üí X} {a : X} :
    Tendsto u atTop (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) a < Œµ :=
  Metric.tendsto_atTop

example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} :
    Continuous f ‚Üî
      ‚àÄ x : X, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x', dist x' x < Œ¥ ‚Üí dist (f x') (f x) < Œµ :=
  Metric.continuous_iff
-- QUOTE.

/- TEXT:
.. index:: continuity, tactics ; continuity


A *lot* of lemmas have some continuity assumptions, so we end up proving a lot of continuity results and there
is a ``continuity`` tactic devoted to this task. Let's prove a continuity statement that will be needed
in an exercise below. Notice that Lean knows how to treat a product of two metric spaces as a metric space, so
it makes sense to consider continuous functions from ``X √ó X`` to ``‚Ñù``.
In particular the (uncurried version of the) distance function is such a function.

BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} (hf : Continuous f) :
    Continuous fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2) := by continuity
-- QUOTE.

/- TEXT:
This tactic is a bit slow, so it is also useful to know
how to do it by hand. We first need to use that ``fun p : X √ó X ‚Ü¶ f p.1`` is continuous because it
is the composition of ``f``, which is continuous by assumption ``hf``, and the projection ``prod.fst`` whose continuity
is the content of the lemma ``continuous_fst``. The composition property is ``Continuous.comp`` which is
in the ``Continuous`` namespace so we can use dot notation to compress
``Continuous.comp hf continuous_fst`` into ``hf.comp continuous_fst`` which is actually more readable
since it really reads as composing our assumption and our lemma.
We can do the same for the second component to get continuity of ``fun p : X √ó X ‚Ü¶ f p.2``. We then assemble
those two continuities using ``Continuous.prod_mk`` to get
``(hf.comp continuous_fst).prod_mk (hf.comp continuous_snd) : Continuous (fun p : X √ó X ‚Ü¶ (f p.1, f p.2))``
and compose once more to get our full proof.
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} (hf : Continuous f) :
    Continuous fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2) :=
  continuous_dist.comp ((hf.comp continuous_fst).prod_mk (hf.comp continuous_snd))
-- QUOTE.

/- TEXT:
The combination of ``Continuous.prod_mk`` and ``continuous_dist`` via ``Continuous.comp`` feels clunky,
even when heavily using dot notation as above. A more serious issue is that this nice proof requires a lot of
planning. Lean accepts the above proof term because it is a full term proving a statement which is
definitionally equivalent to our goal, the crucial definition to unfold being that of a composition of functions.
Indeed our target function ``fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2)`` is not presented as a composition.
The proof term we provided proves continuity of ``dist ‚àò (fun p : X √ó X ‚Ü¶ (f p.1, f p.2))`` which happens
to be definitionally equal to our target function. But if we try to build this proof gradually using
tactics starting with ``apply continuous_dist.comp`` then Lean's elaborator will fail to recognize a
composition and refuse to apply this lemma. It is especially bad at this when products of types are involved.

A better lemma to apply here is
``Continuous.dist {f g : X ‚Üí Y} : Continuous f ‚Üí Continuous g ‚Üí Continuous (fun x ‚Ü¶ dist (f x) (g x))``
which is nicer to Lean's elaborator and also provides a shorter proof when directly providing a full
proof term, as can be seen from the following two new proofs of the above statement:
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} (hf : Continuous f) :
    Continuous fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2) := by
  apply Continuous.dist
  exact hf.comp continuous_fst
  exact hf.comp continuous_snd

example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} (hf : Continuous f) :
    Continuous fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2) :=
  (hf.comp continuous_fst).dist (hf.comp continuous_snd)
-- QUOTE.

/- TEXT:
Note that, without the elaboration issue coming from composition, another way to compress
our proof would be to use ``Continuous.prod_map`` which is sometimes useful and gives
as an alternate proof term ``continuous_dist.comp (hf.prod_map hf)`` which even shorter to type.

Since it is sad to decide between a version which is better for elaboration and a version which is shorter
to type, let us wrap this discussion with a last bit of compression offered
by ``Continuous.fst'`` which allows to compress ``hf.comp continuous_fst`` to ``hf.fst'`` (and the same with ``snd``)
and get our final proof, now bordering obfuscation.

BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] {f : X ‚Üí Y} (hf : Continuous f) :
    Continuous fun p : X √ó X ‚Ü¶ dist (f p.1) (f p.2) :=
  hf.fst'.dist hf.snd'
-- QUOTE.

/- TEXT:
It's your turn now to prove some continuity lemma. After trying the continuity tactic, you will need
``Continuous.add``, ``continuous_pow`` and ``continuous_id`` to do it by hand.

BOTH: -/
-- QUOTE:
example {f : ‚Ñù ‚Üí X} (hf : Continuous f) : Continuous fun x : ‚Ñù ‚Ü¶ f (x ^ 2 + x) :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {f : ‚Ñù ‚Üí X} (hf : Continuous f) : Continuous fun x : ‚Ñù ‚Ü¶ f (x ^ 2 + x) :=
  hf.comp <| (continuous_pow 2).add continuous_id

/- TEXT:
So far we saw continuity as a global notion, but one can also define continuity at a point.
BOTH: -/
-- QUOTE:
example {X Y : Type*} [MetricSpace X] [MetricSpace Y] (f : X ‚Üí Y) (a : X) :
    ContinuousAt f a ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x}, dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ :=
  Metric.continuousAt_iff
-- QUOTE.

/- TEXT:

Balls, open sets and closed sets
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Once we have a distance function, the most important geometric definitions are (open) balls and closed balls.

BOTH: -/
-- QUOTE:
variable (r : ‚Ñù)

example : Metric.ball a r = { b | dist b a < r } :=
  rfl

example : Metric.closedBall a r = { b | dist b a ‚â§ r } :=
  rfl
-- QUOTE.

/- TEXT:
Note that `r` is any real number here, there is no sign restriction. Of course some statements do require a radius condition.
BOTH: -/
-- QUOTE:
example (hr : 0 < r) : a ‚àà Metric.ball a r :=
  Metric.mem_ball_self hr

example (hr : 0 ‚â§ r) : a ‚àà Metric.closedBall a r :=
  Metric.mem_closedBall_self hr
-- QUOTE.

/- TEXT:
Once we have balls, we can define open sets. They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition in terms of balls.

BOTH: -/
-- QUOTE:
example (s : Set X) : IsOpen s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, Metric.ball x Œµ ‚äÜ s :=
  Metric.isOpen_iff
-- QUOTE.

/- TEXT:
Then closed sets are sets whose complement is open. Their important property is they are closed under limits. The closure of a set is the smallest closed set containing it.
BOTH: -/
-- QUOTE:
example {s : Set X} : IsClosed s ‚Üî IsOpen (s·∂ú) :=
  isOpen_compl_iff.symm

example {s : Set X} (hs : IsClosed s) {u : ‚Ñï ‚Üí X} (hu : Tendsto u atTop (ùìù a))
    (hus : ‚àÄ n, u n ‚àà s) : a ‚àà s :=
  hs.mem_of_tendsto hu (eventually_of_forall hus)

example {s : Set X} : a ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, a ‚àà Metric.ball b Œµ :=
  Metric.mem_closure_iff
-- QUOTE.

/- TEXT:
Do the next exercise without using `mem_closure_iff_seq_limit`
BOTH: -/
-- QUOTE:
example {u : ‚Ñï ‚Üí X} (hu : Tendsto u atTop (ùìù a)) {s : Set X} (hs : ‚àÄ n, u n ‚àà s) :
    a ‚àà closure s :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {u : ‚Ñï ‚Üí X} (hu : Tendsto u atTop (ùìù a)) {s : Set X} (hs : ‚àÄ n, u n ‚àà s) : a ‚àà closure s := by
  rw [Metric.tendsto_atTop] at hu
  rw [Metric.mem_closure_iff]
  intro Œµ Œµ_pos
  rcases hu Œµ Œµ_pos with ‚ü®N, hN‚ü©
  refine' ‚ü®u N, hs _, _‚ü©
  rw [dist_comm]
  exact hN N le_rfl

/- TEXT:

Remember from the filters sections that neighborhood filters play a big role in Mathlib.
In the metric space context, the crucial point is that balls provide bases for those filters.
The main lemmas here are ``Metric.nhds_basis_ball`` and ``Metric.nhds_basis_closedBall``
that claim this for open and closed balls with positive radius. The center point is an implicit
argument so we can invoke ``Filter.HasBasis.mem_iff`` as in the following example.

BOTH: -/
-- QUOTE:
example {x : X} {s : Set X} : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, Metric.ball x Œµ ‚äÜ s :=
  Metric.nhds_basis_ball.mem_iff

example {x : X} {s : Set X} : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, Metric.closedBall x Œµ ‚äÜ s :=
  Metric.nhds_basis_closedBall.mem_iff
-- QUOTE.

/- TEXT:

Compactness
^^^^^^^^^^^

Compactness is an important topological notion. It distinguishes subsets of a metric space
that enjoy the same kind of properties as segments in reals compared to other intervals:

* Any sequence taking value in a compact set has a subsequence that converges in this set
* Any continuous function on a nonempty compact set with values in real numbers is bounded and
  achieves its bounds somewhere (this is called the extreme values theorem).
* Compact sets are closed sets.

Let us first check that the unit interval in reals is indeed a compact set, and then check the above
claims for compact sets in general metric spaces. In the second statement we only
need continuity on the given set so we will use ``ContinuousOn`` instead of ``Continuous``, and
we will give separate statements for the minimum and the maximum. Of course all these results
are deduced from more general versions, some of which will be discussed in later sections.

BOTH: -/
-- QUOTE:
example : IsCompact (Set.Icc 0 1 : Set ‚Ñù) :=
  isCompact_Icc

example {s : Set X} (hs : IsCompact s) {u : ‚Ñï ‚Üí X} (hu : ‚àÄ n, u n ‚àà s) :
    ‚àÉ a ‚àà s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, StrictMono œÜ ‚àß Tendsto (u ‚àò œÜ) atTop (ùìù a) :=
  hs.tendsto_subseq hu

example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X ‚Üí ‚Ñù}
      (hfs : ContinuousOn f s) :
    ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y :=
  hs.exists_forall_le hs' hfs

example {s : Set X} (hs : IsCompact s) (hs' : s.Nonempty) {f : X ‚Üí ‚Ñù}
      (hfs : ContinuousOn f s) :
    ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f y ‚â§ f x :=
  hs.exists_forall_ge hs' hfs

example {s : Set X} (hs : IsCompact s) : IsClosed s :=
  hs.isClosed
-- QUOTE.

/- TEXT:

We can also specify that a metric spaces is globally compact, using an extra ``Prop``-valued type class:

BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] [CompactSpace X] : IsCompact (univ : Set X) :=
  isCompact_univ
-- QUOTE.

/- TEXT:

In a compact metric space any closed set is compact, this is ``IsClosed.isCompact``.

BOTH: -/
#check IsCompact.isClosed

/- TEXT:
Uniformly continuous functions
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

We now turn to uniformity notions on metric spaces : uniformly continuous functions, Cauchy sequences and completeness.
Again those are defined in a more general context but we have lemmas in the metric name space to access their elementary definitions.
We start with uniform continuity.

BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] {Y : Type*} [MetricSpace Y] {f : X ‚Üí Y} :
    UniformContinuous f ‚Üî
      ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : X}, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
  Metric.uniformContinuous_iff
-- QUOTE.

/- TEXT:
In order to practice manipulating all those definitions, we will prove that continuous
functions from a compact metric space to a metric space are uniformly continuous
(we will see a more general version in a later section).

We will first give an informal sketch. Let ``f : X ‚Üí Y`` be a continuous function from
a compact metric space to a metric space.
We fix ``Œµ > 0`` and start looking for some ``Œ¥``.

Let ``œÜ : X √ó X ‚Üí ‚Ñù := fun p ‚Ü¶ dist (f p.1) (f p.2)`` and let ``K := { p : X √ó X | Œµ ‚â§ œÜ p }``.
Observe ``œÜ`` is continuous since ``f`` and distance are continuous.
And ``K`` is clearly closed (use ``isClosed_le``) hence compact since ``X`` is compact.

Then we discuss two possibilities using ``eq_empty_or_nonempty``.
If ``K`` is empty then we are clearly done (we can set ``Œ¥ = 1`` for instance).
So let's assume ``K`` is not empty, and use the extreme value theorem to choose ``(x‚ÇÄ, x‚ÇÅ)`` attaining the infimum
of the distance function on ``K``. We can then set ``Œ¥ = dist x‚ÇÄ x‚ÇÅ`` and check everything works.

BOTH: -/
-- QUOTE:
example {X : Type*} [MetricSpace X] [CompactSpace X]
      {Y : Type*} [MetricSpace Y] {f : X ‚Üí Y}
    (hf : Continuous f) : UniformContinuous f :=
  sorry
-- QUOTE.

-- SOLUTIONS:
example {X : Type*} [MetricSpace X] [CompactSpace X] {Y : Type*} [MetricSpace Y] {f : X ‚Üí Y}
    (hf : Continuous f) : UniformContinuous f := by
  rw [Metric.uniformContinuous_iff]
  intro Œµ Œµ_pos
  let œÜ : X √ó X ‚Üí ‚Ñù := fun p ‚Ü¶ dist (f p.1) (f p.2)
  have œÜ_cont : Continuous œÜ := hf.fst'.dist hf.snd'
  let K := { p : X √ó X | Œµ ‚â§ œÜ p }
  have K_closed : IsClosed K := isClosed_le continuous_const œÜ_cont
  have K_cpct : IsCompact K := K_closed.isCompact
  rcases eq_empty_or_nonempty K with hK | hK
  ¬∑ use 1, by norm_num
    intro x y _
    have : (x, y) ‚àâ K := by simp [hK]
    simpa [K] using this
  ¬∑ rcases K_cpct.exists_forall_le hK continuous_dist.continuousOn with ‚ü®‚ü®x‚ÇÄ, x‚ÇÅ‚ü©, xx_in, H‚ü©
    use dist x‚ÇÄ x‚ÇÅ
    constructor
    ¬∑ change _ < _
      rw [dist_pos]
      intro h
      have : Œµ ‚â§ 0 := by simpa [K, œÜ, *] using xx_in
      linarith
    ¬∑ intro x x'
      contrapose!
      intro hxx'
      exact H (x, x') hxx'

/- TEXT:
Completeness
^^^^^^^^^^^^

A Cauchy sequence in a metric space is a sequence whose terms get closer and closer to each other.
There are a couple of equivalent ways to state that idea.
In particular converging sequences are Cauchy. The converse is true only in so-called *complete*
spaces.


BOTH: -/
-- QUOTE:
example (u : ‚Ñï ‚Üí X) :
    CauchySeq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N : ‚Ñï, ‚àÄ m ‚â• N, ‚àÄ n ‚â• N, dist (u m) (u n) < Œµ :=
  Metric.cauchySeq_iff

example (u : ‚Ñï ‚Üí X) :
    CauchySeq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N : ‚Ñï, ‚àÄ n ‚â• N, dist (u n) (u N) < Œµ :=
  Metric.cauchySeq_iff'

example [CompleteSpace X] (u : ‚Ñï ‚Üí X) (hu : CauchySeq u) :
    ‚àÉ x, Tendsto u atTop (ùìù x) :=
  cauchySeq_tendsto_of_complete hu
-- QUOTE.

/- TEXT:

We'll practice using this definition by proving a convenient criterion which is a special case of a
criterion appearing in Mathlib. This is also a good opportunity to practice using big sums in
a geometric context. In addition to the explanations from the filters section, you will probably need
``tendsto_pow_atTop_nhds_0_of_lt_1``, ``Tendsto.mul`` and ``dist_le_range_sum_dist``.
BOTH: -/
open BigOperators

open Finset

-- QUOTE:
theorem cauchySeq_of_le_geometric_two' {u : ‚Ñï ‚Üí X}
    (hu : ‚àÄ n : ‚Ñï, dist (u n) (u (n + 1)) ‚â§ (1 / 2) ^ n) : CauchySeq u := by
  rw [Metric.cauchySeq_iff']
  intro Œµ Œµ_pos
  obtain ‚ü®N, hN‚ü© : ‚àÉ N : ‚Ñï, 1 / 2 ^ N * 2 < Œµ := by sorry
  use N
  intro n hn
  obtain ‚ü®k, rfl : n = N + k‚ü© := le_iff_exists_add.mp hn
  calc
    dist (u (N + k)) (u N) = dist (u (N + 0)) (u (N + k)) := sorry
    _ ‚â§ ‚àë i in range k, dist (u (N + i)) (u (N + (i + 1))) := sorry
    _ ‚â§ ‚àë i in range k, (1 / 2 : ‚Ñù) ^ (N + i) := sorry
    _ = 1 / 2 ^ N * ‚àë i in range k, (1 / 2 : ‚Ñù) ^ i := sorry
    _ ‚â§ 1 / 2 ^ N * 2 := sorry
    _ < Œµ := sorry

-- QUOTE.

-- SOLUTIONS:
example {u : ‚Ñï ‚Üí X} (hu : ‚àÄ n : ‚Ñï, dist (u n) (u (n + 1)) ‚â§ (1 / 2) ^ n) : CauchySeq u := by
  rw [Metric.cauchySeq_iff']
  intro Œµ Œµ_pos
  obtain ‚ü®N, hN‚ü© : ‚àÉ N : ‚Ñï, 1 / 2 ^ N * 2 < Œµ := by
    have : Tendsto (fun N : ‚Ñï ‚Ü¶ (1 / 2 ^ N * 2 : ‚Ñù)) atTop (ùìù 0) := by
      rw [‚Üê zero_mul (2 : ‚Ñù)]
      apply Tendsto.mul
      simp_rw [‚Üê one_div_pow (2 : ‚Ñù)]
      apply tendsto_pow_atTop_nhds_0_of_lt_1 <;> linarith
      exact tendsto_const_nhds
    rcases(atTop_basis.tendsto_iff (nhds_basis_Ioo_pos (0 : ‚Ñù))).mp this Œµ Œµ_pos with ‚ü®N, _, hN‚ü©
    exact ‚ü®N, by simpa using (hN N left_mem_Ici).2‚ü©
  use N
  intro n hn
  obtain ‚ü®k, rfl : n = N + k‚ü© := le_iff_exists_add.mp hn
  calc
    dist (u (N + k)) (u N) = dist (u (N + 0)) (u (N + k)) := by rw [dist_comm, add_zero]
    _ ‚â§ ‚àë i in range k, dist (u (N + i)) (u (N + (i + 1))) :=
      (dist_le_range_sum_dist (fun i ‚Ü¶ u (N + i)) k)
    _ ‚â§ ‚àë i in range k, (1 / 2 : ‚Ñù) ^ (N + i) := (sum_le_sum fun i _ ‚Ü¶ hu <| N + i)
    _ = 1 / 2 ^ N * ‚àë i in range k, (1 / 2 : ‚Ñù) ^ i := by simp_rw [‚Üê one_div_pow, pow_add, ‚Üê mul_sum]
    _ ‚â§ 1 / 2 ^ N * 2 :=
      (mul_le_mul_of_nonneg_left (sum_geometric_two_le _)
        (one_div_nonneg.mpr (pow_nonneg (zero_le_two : (0 : ‚Ñù) ‚â§ 2) _)))
    _ < Œµ := hN


/- TEXT:

We are ready for the final boss of this section: Baire's theorem for complete metric spaces!
The proof skeleton below shows interesting techniques. It uses the ``choose`` tactic in its exclamation
mark variant (you should experiment with removing this exclamation mark) and it shows how to
define something inductively in the middle of a proof using ``Nat.rec_on``.

BOTH: -/
-- QUOTE:
open Metric

example [CompleteSpace X] (f : ‚Ñï ‚Üí Set X) (ho : ‚àÄ n, IsOpen (f n)) (hd : ‚àÄ n, Dense (f n)) :
    Dense (‚ãÇ n, f n) := by
  let B : ‚Ñï ‚Üí ‚Ñù := fun n ‚Ü¶ (1 / 2) ^ n
  have Bpos : ‚àÄ n, 0 < B n
  sorry
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Œ¥, Œ¥pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Œ¥`.
    We can also require `radius ‚â§ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have :
    ‚àÄ (n : ‚Ñï) (x : X),
      ‚àÄ Œ¥ > 0, ‚àÉ y : X, ‚àÉ r > 0, r ‚â§ B (n + 1) ‚àß closedBall y r ‚äÜ closedBall x Œ¥ ‚à© f n :=
    by sorry
  choose! center radius Hpos HB Hball using this
  intro x
  rw [mem_closure_iff_nhds_basis nhds_basis_closedBall]
  intro Œµ Œµpos
  /- `Œµ` is positive. We have to find a point in the ball of radius `Œµ` around `x`
    belonging to all `f n`. For this, we construct inductively a sequence
    `F n = (c n, r n)` such that the closed ball `closedBall (c n) (r n)` is included
    in the previous ball and in `f n`, and such that `r n` is small enough to ensure
    that `c n` is a Cauchy sequence. Then `c n` converges to a limit which belongs
    to all the `f n`. -/
  let F : ‚Ñï ‚Üí X √ó ‚Ñù := fun n ‚Ü¶
    Nat.recOn n (Prod.mk x (min Œµ (B 0)))
      fun n p ‚Ü¶ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : ‚Ñï ‚Üí X := fun n ‚Ü¶ (F n).1
  let r : ‚Ñï ‚Üí ‚Ñù := fun n ‚Ü¶ (F n).2
  have rpos : ‚àÄ n, 0 < r n := by sorry
  have rB : ‚àÄ n, r n ‚â§ B n := by sorry
  have incl : ‚àÄ n, closedBall (c (n + 1)) (r (n + 1)) ‚äÜ closedBall (c n) (r n) ‚à© f n := by
    sorry
  have cdist : ‚àÄ n, dist (c n) (c (n + 1)) ‚â§ B n := by sorry
  have : CauchySeq c := cauchySeq_of_le_geometric_two' cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with ‚ü®y, ylim‚ü©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Œµ`.
  use y
  have I : ‚àÄ n, ‚àÄ m ‚â• n, closedBall (c m) (r m) ‚äÜ closedBall (c n) (r n) := by sorry
  have yball : ‚àÄ n, y ‚àà closedBall (c n) (r n) := by sorry
  sorry
-- QUOTE.

-- SOLUTIONS:
example [CompleteSpace X] (f : ‚Ñï ‚Üí Set X) (ho : ‚àÄ n, IsOpen (f n)) (hd : ‚àÄ n, Dense (f n)) :
    Dense (‚ãÇ n, f n) := by
  let B : ‚Ñï ‚Üí ‚Ñù := fun n ‚Ü¶ (1 / 2) ^ n
  have Bpos : ‚àÄ n, 0 < B n := fun n ‚Ü¶ pow_pos sorry n
  /- Translate the density assumption into two functions `center` and `radius` associating
    to any n, x, Œ¥, Œ¥pos a center and a positive radius such that
    `closedBall center radius` is included both in `f n` and in `closedBall x Œ¥`.
    We can also require `radius ‚â§ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have :
    ‚àÄ (n : ‚Ñï) (x : X),
      ‚àÄ Œ¥ > 0, ‚àÉ y : X, ‚àÉ r > 0, r ‚â§ B (n + 1) ‚àß closedBall y r ‚äÜ closedBall x Œ¥ ‚à© f n := by
    intro n x Œ¥ Œ¥pos
    have : x ‚àà closure (f n) := hd n x
    rcases Metric.mem_closure_iff.1 this (Œ¥ / 2) (half_pos Œ¥pos) with ‚ü®y, ys, xy‚ü©
    rw [dist_comm] at xy
    obtain ‚ü®r, rpos, hr‚ü© : ‚àÉ r > 0, closedBall y r ‚äÜ f n :=
      nhds_basis_closedBall.mem_iff.1 (isOpen_iff_mem_nhds.1 (ho n) y ys)
    refine' ‚ü®y, min (min (Œ¥ / 2) r) (B (n + 1)), _, _, fun z hz ‚Ü¶ ‚ü®_, _‚ü©‚ü©
    show 0 < min (min (Œ¥ / 2) r) (B (n + 1))
    exact lt_min (lt_min (half_pos Œ¥pos) rpos) (Bpos (n + 1))
    show min (min (Œ¥ / 2) r) (B (n + 1)) ‚â§ B (n + 1)
    exact min_le_right _ _
    show z ‚àà closedBall x Œ¥
    exact
      calc
        dist z x ‚â§ dist z y + dist y x := dist_triangle _ _ _
        _ ‚â§ min (min (Œ¥ / 2) r) (B (n + 1)) + Œ¥ / 2 := (add_le_add hz xy.le)
        _ ‚â§ Œ¥ / 2 + Œ¥ / 2 := (add_le_add_right ((min_le_left _ _).trans (min_le_left _ _)) _)
        _ = Œ¥ := add_halves Œ¥

    show z ‚àà f n
    exact
      hr
        (calc
          dist z y ‚â§ min (min (Œ¥ / 2) r) (B (n + 1)) := hz
          _ ‚â§ r := (min_le_left _ _).trans (min_le_right _ _)
          )
  choose! center radius Hpos HB Hball using this
  refine' fun x ‚Ü¶ (mem_closure_iff_nhds_basis nhds_basis_closedBall).2 fun Œµ Œµpos ‚Ü¶ _
  /- `Œµ` is positive. We have to find a point in the ball of radius `Œµ` around `x` belonging to all
    `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
    `closedBall (c n) (r n)` is included in the previous ball and in `f n`, and such that
    `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
    limit which belongs to all the `f n`. -/
  let F : ‚Ñï ‚Üí X √ó ‚Ñù := fun n ‚Ü¶
    Nat.recOn n (Prod.mk x (min Œµ (B 0))) fun n p ‚Ü¶ Prod.mk (center n p.1 p.2) (radius n p.1 p.2)
  let c : ‚Ñï ‚Üí X := fun n ‚Ü¶ (F n).1
  let r : ‚Ñï ‚Üí ‚Ñù := fun n ‚Ü¶ (F n).2
  have rpos : ‚àÄ n, 0 < r n := by
    intro n
    induction' n with n hn
    exact lt_min Œµpos (Bpos 0)
    exact Hpos n (c n) (r n) hn
  have rB : ‚àÄ n, r n ‚â§ B n := by
    intro n
    induction' n with n hn
    exact min_le_right _ _
    exact HB n (c n) (r n) (rpos n)
  have incl : ‚àÄ n, closedBall (c (n + 1)) (r (n + 1)) ‚äÜ closedBall (c n) (r n) ‚à© f n := fun n ‚Ü¶
    Hball n (c n) (r n) (rpos n)
  have cdist : ‚àÄ n, dist (c n) (c (n + 1)) ‚â§ B n := by
    intro n
    rw [dist_comm]
    have A : c (n + 1) ‚àà closedBall (c (n + 1)) (r (n + 1)) :=
      mem_closedBall_self (rpos <| n + 1).le
    have I :=
      calc
        closedBall (c (n + 1)) (r (n + 1)) ‚äÜ closedBall (c n) (r n) :=
          (incl n).trans Set.inter_subset_left
        _ ‚äÜ closedBall (c n) (B n) := closedBall_subset_closedBall (rB n)

    exact I A
  have : CauchySeq c := cauchySeq_of_le_geometric_two' cdist
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchySeq_tendsto_of_complete this with ‚ü®y, ylim‚ü©
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Œµ`.
  use y
  have I : ‚àÄ n, ‚àÄ m ‚â• n, closedBall (c m) (r m) ‚äÜ closedBall (c n) (r n) := by
    intro n
    refine' Nat.le_induction _ fun m hnm h ‚Ü¶ _
    ¬∑ exact Subset.rfl
    ¬∑ exact (incl m).trans (Set.inter_subset_left.trans h)
  have yball : ‚àÄ n, y ‚àà closedBall (c n) (r n) := by
    intro n
    refine' isClosed_ball.mem_of_tendsto ylim _
    refine' (Filter.eventually_ge_atTop n).mono fun m hm ‚Ü¶ _
    exact I n m hm (mem_closedBall_self (rpos _).le)
  constructor
  ¬∑ suffices ‚àÄ n, y ‚àà f n by rwa [Set.mem_iInter]
    intro n
    have : closedBall (c (n + 1)) (r (n + 1)) ‚äÜ f n :=
      Subset.trans (incl n) Set.inter_subset_right
    exact this (yball (n + 1))
  calc
    dist y x ‚â§ r 0 := yball 0
    _ ‚â§ Œµ := min_le_left _ _


/- TEXT:

BOTH: -/
