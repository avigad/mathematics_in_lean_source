import Mathlib.Tactic.LibrarySearch
import Mathlib.Algebra.BigOperators.Ring
import Mathlib.Data.Real.Basic

/- TEXT:
.. _section_hierarchies_basics:

Basics
------

At the very bottom of all hierarchies in Lean, we find data-carrying
classes. The following class records that the given type ``Œ±`` is endowed with
a distinguished element called ``one``. At this stage, it has no property at all.
BOTH: -/

-- QUOTE:
class One‚ÇÅ (Œ± : Type) where
  /-- The element one -/
  one : Œ±
-- QUOTE.

/- TEXT:
Since we'll make a much heavier use of classes in this chapter, we need to understand some
more details about what the ``class`` command is doing.
First, the ``class`` command above defines a structure ``One‚ÇÅ`` with parameter ``Œ± : Type`` and
a single field ``one``. It also mark this structure as a class so that arguments of type
``One‚ÇÅ Œ±`` for some type ``Œ±`` will be inferrable using the instance resolution procedure,
as long as they are marked as instance-implicit, ie appear between square brackets.
Those two effects could also have been achieved using the ``structure`` command with ``class``
attribute, ie writing ``@[class] structure`` instance of ``class``. But the class command also
ensures that ``One‚ÇÅ Œ±`` appears as an instance-implicit argument in its own fields. Compare:
BOTH: -/

-- QUOTE:
#check One‚ÇÅ.one -- One‚ÇÅ.one {Œ± : Type} [self : One‚ÇÅ Œ±] : Œ±

@[class] structure One‚ÇÇ (Œ± : Type) where
  /-- The element one -/
  one : Œ±

#check One‚ÇÇ.one
-- QUOTE.

/- TEXT:
In the second check, we can see that ``self : One‚ÇÇ Œ±`` is an explicit argument.
Let us make sure the first version is indeed usable without any explicit argument.
BOTH: -/

-- QUOTE:
example (Œ± : Type) [One‚ÇÅ Œ±] : Œ± := One‚ÇÅ.one
-- QUOTE.

/- TEXT:
Remark: in the above example, the argument ``One‚ÇÅ Œ±`` is marked as instance-implicit,
which is a bit silly since this affects only *uses* of the declaration and declaration created by
the ``example`` command cannot be used. However it allows to avoid giving a name to that
argument and, more importantly, it starts installing the good habit of marking ``One‚ÇÅ Œ±``
arguments as instance-implicit.

Another remark is that all this will work only when Lean knows what is ``Œ±``. In the above
example, leaving out the type ascription ``: Œ±`` would generate an error message like:
``typeclass instance problem is stuck, it is often due to metavariables One‚ÇÅ (?m.263 Œ±)``
where ``?m.263 Œ±`` means "some type depending on ``Œ±``" (and 263 is simply an auto-generated
index that would be useful to distinguish between several unknown things). Another way
to avoid this issue would be to use a type annotation, as in:
BOTH: -/
-- QUOTE:
example (Œ± : Type) [One‚ÇÅ Œ±] := (One‚ÇÅ.one : Œ±)
-- QUOTE.

/- TEXT:
You may have already encountered that issue when playing with limits of sequences
in :numref:`sequences_and_convergence` if you tried to state for instance that
``0 < 1`` without telling Lean whether you meant this inequality to be about natural numbers
or real numbers.

Our next task is to assign a notation to ``One‚ÇÅ.one``. Since we don't want collisions
with the builtin notation for ``1``, we will use ``ùüô``. This is achieved by the following
command where the first line tells Lean to use the documentation
of ``One‚ÇÅ.one`` as documentation for the symbol ``ùüô``.
BOTH: -/
-- QUOTE:
@[inherit_doc]
notation "ùüô" => One‚ÇÅ.one

example {Œ± : Type} [One‚ÇÅ Œ±] : Œ± := ùüô

example {Œ± : Type} [One‚ÇÅ Œ±] : (ùüô : Œ±) = ùüô := rfl
-- QUOTE.

/- TEXT:
We now want a data-carrying class recording a binary operation. We don't want to choose
between addition and multiplication for now so we'll use diamond.
BOTH: -/

-- QUOTE:
class Dia‚ÇÅ (Œ± : Type) where
  dia : Œ± ‚Üí Œ± ‚Üí Œ±

infixl:70 " ‚ãÑ "   => Dia‚ÇÅ.dia
-- QUOTE.

/- TEXT:
As in the ``One‚ÇÅ`` example, the operation has no property at all at this stage. Let us
now define the class of semigroup structures where the operation is denoted by ``‚ãÑ``.
For now, we define it by hand as a structure with two fields, a ``Dia‚ÇÅ`` instance and some
``Prop``-valued field ``dia_assoc`` asserting associativity of ``‚ãÑ``.
BOTH: -/

-- QUOTE:
class Semigroup‚ÇÅ (Œ± : Type) where
  toDia‚ÇÅ : Dia‚ÇÅ Œ±
  /-- Diamond is associative -/
  dia_assoc : ‚àÄ a b c : Œ±, a ‚ãÑ b ‚ãÑ c = a ‚ãÑ (b ‚ãÑ c)
-- QUOTE.

/- TEXT:
Note that while stating `dia_assoc`, the previously defined field `toDia‚ÇÅ` is in the local
context hence can be used when Lean searches for an instance of `Dia‚ÇÅ Œ±` to make sense
of `a ‚ãÑ b`. However this `toDia‚ÇÅ` field does not become part of the type class instances database.
Hence doing ``example {Œ± : Type} [Semigroup‚ÇÅ Œ±] (a b : Œ±) : Œ± := a ‚ãÑ b`` would fail with
error message ``failed to synthesize instance Dia‚ÇÅ Œ±``.

We can fix this by adding the ``instance`` attribute later.
BOTH: -/

-- QUOTE:
attribute [instance] Semigroup‚ÇÅ.toDia‚ÇÅ

example {Œ± : Type} [Semigroup‚ÇÅ Œ±] (a b : Œ±) : Œ± := a ‚ãÑ b
-- QUOTE.

/- TEXT:
Before building up, we need a more convenient way to extend structures than explicitly
writing fields like `toDia‚ÇÅ` and adding the instance attribute by hand. The ``class``
supports this using the ``extends`` syntax as in:
BOTH: -/

-- QUOTE:
class Semigroup‚ÇÇ (Œ± : Type) extends Dia‚ÇÅ Œ± where
  /-- Diamond is associative -/
  dia_assoc : ‚àÄ a b c : Œ±, a ‚ãÑ b ‚ãÑ c = a ‚ãÑ (b ‚ãÑ c)

example {Œ± : Type} [Semigroup‚ÇÇ Œ±] (a b : Œ±) : Œ± := a ‚ãÑ b
-- QUOTE.

/- TEXT:
Note this syntax is also available in the ``structure`` command, although it that
case it fixes only the hurdle of writing fields such as `toDia‚ÇÅ` since there
is no instance to define in that case.


Let us now try to combine a diamond operation and a distinguished one with axioms saying
this element is neutral on both sides.
BOTH: -/
-- QUOTE:
class DiaOneClass‚ÇÅ (Œ± : Type) extends One‚ÇÅ Œ±, Dia‚ÇÅ Œ± where
  /-- One is a left neutral element for diamond. -/
  one_dia : ‚àÄ a : Œ±, ùüô ‚ãÑ a = a
  /-- One is a right neutral element for diamond -/
  dia_one : ‚àÄ a : Œ±, a ‚ãÑ ùüô = a

-- QUOTE.

/- TEXT:
In the next example, we tell Lean that ``Œ±`` has a ``DiaOneClass‚ÇÅ`` structure and state a
property that uses both a `Dia‚ÇÅ` instance and a `One‚ÇÅ` instance. In order to see how Lean finds
those instances we set a tracing option whose result can be seen in the info view. This result
is rather terse by default but can be expended by clicking one lines ending with black arrows.
It includes failed attempts where Lean tried to find instances before having enough type
information to succeed. The successful attempts do involve the instances generated by the
``extends`` syntax.
BOTH: -/

-- QUOTE:
set_option trace.Meta.synthInstance true in
example {Œ± : Type} [DiaOneClass‚ÇÅ Œ±] (a b : Œ±) : Prop := a ‚ãÑ b = ùüô
-- QUOTE.

/- TEXT:
Note that we don't need to include extra fields where combining existing classes. Hence we can
define monoids as:
BOTH: -/

-- QUOTE:
class Monoid‚ÇÅ (Œ± : Type) extends Semigroup‚ÇÅ Œ±, DiaOneClass‚ÇÅ Œ±
-- QUOTE.

/- TEXT:
While the above definition seems straightforward, it hides an important subtlety. Both
``Semigroup‚ÇÅ Œ±`` and ``DiaOneClass‚ÇÅ Œ±`` extend ``Dia‚ÇÅ Œ±``, so one could fear that having
a ``Monoid‚ÇÅ Œ±`` instance gives two unrelated diamond operations on ``Œ±``, one coming from
a field ``Monoid‚ÇÅ.toSemigroup‚ÇÅ`` and one coming from a field ``Monoid‚ÇÅ.toDiaOneClass‚ÇÅ``.

Indeed if we try to build a monoid class by hand using:
BOTH: -/

-- QUOTE:
class Monoid‚ÇÇ (Œ± : Type) where
  toSemigroup‚ÇÅ : Semigroup‚ÇÅ Œ±
  toDiaOneClass‚ÇÅ : DiaOneClass‚ÇÅ Œ±
-- QUOTE.

/- TEXT:
then we get two completely unrelated diamond operations
``Monoid‚ÇÇ.toSemigroup‚ÇÅ.toDia‚ÇÅ.dia`` and ``Monoid‚ÇÇ.toDiaOneClass‚ÇÅ.toDia‚ÇÅ.dia``.

The version generated using the ``extends`` syntax does not have this defect.
BOTH: -/

-- QUOTE:
example {Œ± : Type} [Monoid‚ÇÅ Œ±] :
  (Monoid‚ÇÅ.toSemigroup‚ÇÅ.toDia‚ÇÅ.dia : Œ± ‚Üí Œ± ‚Üí Œ±) = Monoid‚ÇÅ.toDiaOneClass‚ÇÅ.toDia‚ÇÅ.dia := rfl
-- QUOTE.

/- TEXT:
So the ``class`` command did some magic for us (and the ``structure`` command would have done it
too). An easy way to see what are the fields of our classes is to check their constructor. Compare:
BOTH: -/

-- QUOTE:
/- Monoid‚ÇÇ.mk {Œ± : Type} (toSemigroup‚ÇÅ : Semigroup‚ÇÅ Œ±) (toDiaOneClass‚ÇÅ : DiaOneClass‚ÇÅ Œ±) : Monoid‚ÇÇ Œ± -/
#check Monoid‚ÇÇ.mk

/- Monoid‚ÇÅ.mk {Œ± : Type} [toSemigroup‚ÇÅ : Semigroup‚ÇÅ Œ±] [toOne‚ÇÅ : One‚ÇÅ Œ±] (one_dia : ‚àÄ (a : Œ±), ùüô ‚ãÑ a = a) (dia_one : ‚àÄ (a : Œ±), a ‚ãÑ ùüô = a) : Monoid‚ÇÅ Œ± -/
#check Monoid‚ÇÅ.mk
-- QUOTE.

/- TEXT:
So we see that ``Monoid‚ÇÅ`` takes ``Semigroup‚ÇÅ Œ±`` argument as expected but then it won't
take a would-be overlapping ``DiaOneClass‚ÇÅ Œ±`` argument but instead tears it apart and includes
only the non-overlapping parts. And it also auto-generated an instance ``Monoid‚ÇÅ.toDiaOneClass‚ÇÅ``
which is *not* a field but has the expected signature which, from the end-user point of view,
restores the symmetry between the two extended classes ``Semigroup‚ÇÅ`` and ``DiaOneClass‚ÇÅ``.
BOTH: -/

-- QUOTE:
#check Monoid‚ÇÅ.toSemigroup‚ÇÅ
#check Monoid‚ÇÅ.toDiaOneClass‚ÇÅ
-- QUOTE.

/- TEXT:
We are now very close to defining groups. We could add to the monoid structure a field asserting
the existence of an inverse for every element. But then we would need to work to access these
inverses. In practice it is more convenient to add it as data. To optimize reusability,
we define a new data-carrying class, and then give it some notation.
BOTH: -/

-- QUOTE:
class Inv‚ÇÅ (Œ± : Type) where
  /-- The inversion function -/
  inv : Œ± ‚Üí Œ±

@[inherit_doc]
postfix:max "‚Åª¬π" => Inv‚ÇÅ.inv

class Group‚ÇÅ (G : Type) extends Monoid‚ÇÅ G, Inv G where
  inv_dia : ‚àÄ a : G, a‚Åª¬π ‚ãÑ a = ùüô
-- QUOTE.

/- TEXT:
The above definition may seem too weak, we only ask that ``a‚Åª¬π`` is a left-inverse of ``a``.
But the other side is automatic. In order to prove that, we need a preliminary lemma.
BOTH: -/

-- QUOTE:
lemma left_inv_eq_right_inv‚ÇÅ {M : Type} [Monoid‚ÇÅ M] {a b c : M} (hba : b ‚ãÑ a = ùüô) (hac : a ‚ãÑ c = ùüô) : b = c := by
  rw [‚Üê DiaOneClass‚ÇÅ.one_dia c, ‚Üê hba, Semigroup‚ÇÅ.dia_assoc, hac, DiaOneClass‚ÇÅ.dia_one b]
-- QUOTE.

/- TEXT:
In this lemma, it is pretty annoying to give full names, especially since it requires knowing
which part of the hierarchy provides those facts. One way to fix this is to use the ``export``
command to copy those facts as lemmas in the root name space.
BOTH: -/

-- QUOTE:
export DiaOneClass‚ÇÅ (one_dia dia_one)
export Semigroup‚ÇÅ (dia_assoc)
export Group‚ÇÅ (inv_dia)
-- QUOTE.

/- TEXT:
We can then rewrite the above proof as:
BOTH: -/

-- QUOTE:
example {M : Type} [Monoid‚ÇÅ M] {a b c : M} (hba : b ‚ãÑ a = ùüô) (hac : a ‚ãÑ c = ùüô) : b = c := by
  rw [‚Üê one_dia c, ‚Üê hba, dia_assoc, hac, dia_one b]
-- QUOTE.

/- TEXT:
It is now your turn to prove things about our algebraic structures.
BOTH: -/

-- QUOTE:
lemma inv_eq_of_dia [Group‚ÇÅ G] {a b : G} (h : a ‚ãÑ b = ùüô) : a‚Åª¬π = b :=
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  left_inv_eq_right_inv‚ÇÅ (inv_dia a) h
-- BOTH:

lemma dia_inv [Group‚ÇÅ G] (a : G) : a ‚ãÑ a‚Åª¬π = ùüô :=
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  by rw [‚Üê inv_dia a‚Åª¬π, inv_eq_of_dia (inv_dia a)]
-- QUOTE.

/- TEXT:
At this stage we would like to move on to define rings, but there is a serious issue.
A ring structure on a type contains both an additive group structure and a multiplicative
monoid structure, and some properties about their interaction. But so far we hard-coded
a notation ``‚ãÑ`` for all our operations. More fundamentally, the type class system
assumes every type has only one instance of each type class. There are various
ways to solve this issue. Surprisingly mathlib uses the naive idea to duplicate
everything for additive and multiplicative theories with the help of some code-generating
attribute. Structures and classes are defined in both additive and multiplicative notation
with an attribute ``to_additive`` linking them. In case of multiple inheritance like for
semi-groups, the auto-generated "symmetry-restoring" instances need also to be marked.
This is a bit technical you don't need to understand details. The important point is that
lemmas are then only stated in multiplicative notation and marked with the attribute ``to_additive``
to generate the additive version as ``left_inv_eq_right_inv'`` with it's auto-generated additive
version ``left_neg_eq_right_neg'``. In order to check the name of this additive version we
used the ``whatsnew in`` command on top of ``left_inv_eq_right_inv'``.
BOTH: -/

-- QUOTE:



class AddSemigroup‚ÇÉ (Œ± : Type) extends Add Œ± where
/-- Addition is associative -/
  add_assoc‚ÇÉ : ‚àÄ a b c : Œ±, a + b + c = a + (b + c)

@[to_additive AddSemigroup‚ÇÉ]
class Semigroup‚ÇÉ (Œ± : Type) extends Mul Œ± where
/-- Multiplication is associative -/
  mul_assoc‚ÇÉ : ‚àÄ a b c : Œ±, a * b * c = a * (b * c)

class AddMonoid‚ÇÉ (Œ± : Type) extends AddSemigroup‚ÇÉ Œ±, AddZeroClass Œ±

@[to_additive AddMonoid‚ÇÉ]
class Monoid‚ÇÉ (Œ± : Type) extends Semigroup‚ÇÉ Œ±, MulOneClass Œ±

attribute [to_additive existing] Monoid‚ÇÉ.toMulOneClass

export Semigroup‚ÇÉ (mul_assoc‚ÇÉ)
export AddSemigroup‚ÇÉ (add_assoc‚ÇÉ)

whatsnew in
@[to_additive]
lemma left_inv_eq_right_inv' {M : Type} [Monoid‚ÇÉ M] {a b c : M} (hba : b * a = 1) (hac : a * c = 1) : b = c := by
  rw [‚Üê one_mul c, ‚Üê hba, mul_assoc‚ÇÉ, hac, mul_one b]

#check left_neg_eq_right_neg'
-- QUOTE.

/- TEXT:
Equipped with this technology, we can easily define also commutative semigroups, monoids and
groups, and then define rings.

BOTH: -/
-- QUOTE:
class AddCommSemigroup‚ÇÉ (Œ± : Type) extends AddSemigroup‚ÇÉ Œ± where
  add_comm : ‚àÄ a b : Œ±, a + b = b + a

@[to_additive AddCommSemigroup‚ÇÉ]
class CommSemigroup‚ÇÉ (Œ± : Type) extends Semigroup‚ÇÉ Œ± where
  mul_comm : ‚àÄ a b : Œ±, a * b = b * a

class AddCommMonoid‚ÇÉ (Œ± : Type) extends AddMonoid‚ÇÉ Œ±, AddCommSemigroup‚ÇÉ Œ±

@[to_additive AddCommMonoid‚ÇÉ]
class CommMonoid‚ÇÉ (Œ± : Type) extends Monoid‚ÇÉ Œ±, CommSemigroup‚ÇÉ Œ±

class AddGroup‚ÇÉ (G : Type) extends AddMonoid‚ÇÉ G, Neg G where
  neg_add : ‚àÄ a : G, -a + a = 0

@[to_additive AddGroup‚ÇÉ]
class Group‚ÇÉ (G : Type) extends Monoid‚ÇÉ G, Inv G where
  inv_mul : ‚àÄ a : G, a‚Åª¬π * a = 1
-- QUOTE.

/- TEXT:
We should remember to tagged lemmas with ``simp`` when appropriate.
BOTH: -/

-- QUOTE:
attribute [simp] Group‚ÇÉ.inv_mul AddGroup‚ÇÉ.neg_add

-- QUOTE.

/- TEXT:
Then we need to repeat ourselves a bit since we switch to standard notations, but at least
``to_additive`` does the work of translating from the multiplicative notation to the additive one.
BOTH: -/

-- QUOTE:
@[to_additive]
lemma inv_eq_of_mul [Group‚ÇÉ G] {a b : G} (h : a * b = 1) : a‚Åª¬π = b :=
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  left_inv_eq_right_inv' (Group‚ÇÉ.inv_mul a) h
-- BOTH:
-- QUOTE.

/- TEXT:
Note that ``to_additive`` can be ask to tag a lemma with ``simp`` and propagate that attribute
to the additive version as follows.
BOTH: -/

-- QUOTE:
@[to_additive (attr := simp)]
lemma Group‚ÇÉ.mul_inv {G : Type} [Group‚ÇÉ G] {a : G} : a * a‚Åª¬π = 1 := by
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  rw [‚Üê inv_mul a‚Åª¬π, inv_eq_of_mul (inv_mul a)]
-- BOTH:

@[to_additive]
lemma mul_left_cancel‚ÇÉ {G : Type} [Group‚ÇÉ G] {a b c : G} (h : a * b = a * c) : b = c := by
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  simpa [‚Üê mul_assoc‚ÇÉ] using congr_arg (a‚Åª¬π * ¬∑) h
-- BOTH:

@[to_additive]
lemma mul_right_cancel‚ÇÉ {G : Type} [Group‚ÇÉ G] {a b c : G} (h : b*a = c*a) : b = c := by
/- EXAMPLES:
  sorry
SOLUTIONS: -/
  simpa [mul_assoc‚ÇÉ] using congr_arg (¬∑ * a‚Åª¬π) h
-- BOTH:

class AddCommGroup‚ÇÉ (G : Type) extends AddGroup‚ÇÉ G, AddCommMonoid‚ÇÉ G

@[to_additive AddCommGroup‚ÇÉ]
class CommGroup‚ÇÉ (G : Type) extends Group‚ÇÉ G, CommMonoid‚ÇÉ G

-- QUOTE.

/- TEXT:
We are now ready for rings. For demonstration purposes we won't assume that addition is
commutative, and then immediately provide an instance of ``AddCommGroup‚ÇÉ``. Mathlib does not
play this game, first because in practice this does not make any ring instance easier and
also because Mathlib's algebraic hierarchy goes through semi-rings which are like rings but without
opposites so that the proof below does not work for them. What we gain here, besides a nice exercise
if you have never seen it, is an example of building an instance using the syntax that allows
to provide a parent structure and some extra fields.
BOTH: -/

-- QUOTE:
class Ring‚ÇÉ (R : Type) extends AddGroup‚ÇÉ R, Monoid‚ÇÉ R, MulZeroClass R where
  /-- Multiplication is left distributive over addition -/
  left_distrib : ‚àÄ a b c : R, a * (b + c) = a * b + a * c
  /-- Multiplication is right distributive over addition -/
  right_distrib : ‚àÄ a b c : R, (a + b) * c = a * c + b * c

instance {R : Type} [Ring‚ÇÉ R] : AddCommGroup‚ÇÉ R :=
{ Ring‚ÇÉ.toAddGroup‚ÇÉ with
  add_comm := by
/- EXAMPLES:
    sorry }
SOLUTIONS: -/
    intro a b
    have : a + (a + b + b) = a + (b + a + b) := calc
      a + (a + b + b) = (a + a) + (b + b) := by simp [add_assoc‚ÇÉ, add_assoc‚ÇÉ]
      _ = (1 * a + 1 * a) + (1 * b + 1 * b) := by simp
      _ = (1 + 1) * a + (1 + 1) * b := by simp [Ring‚ÇÉ.right_distrib]
      _ = (1 + 1) * (a + b) := by simp [Ring‚ÇÉ.left_distrib]
      _ = 1 * (a + b) + 1 * (a + b) := by simp [Ring‚ÇÉ.right_distrib]
      _ = (a + b) + (a + b) := by simp
      _ = a + (b + a + b) := by simp [add_assoc‚ÇÉ]
    exact add_right_cancel‚ÇÉ (add_left_cancel‚ÇÉ this) }
-- QUOTE.
/- TEXT:
Of course we can also build concrete instances, such as a ring structure on integers (of course
the instance below uses that all the work is already done in Mathlib).
BOTH: -/

-- QUOTE:
instance : Ring‚ÇÉ ‚Ñ§ where
  add := (¬∑ + ¬∑)
  add_assoc‚ÇÉ := add_assoc
  zero := 0
  zero_add := by simp
  add_zero := by simp
  neg := (- ¬∑)
  neg_add := by simp
  mul := (¬∑ * ¬∑)
  mul_assoc‚ÇÉ := mul_assoc
  one := 1
  one_mul := by simp
  mul_one := by simp
  zero_mul := by simp
  mul_zero := by simp
  left_distrib := Int.mul_add
  right_distrib := Int.add_mul
-- QUOTE.
/- TEXT:
As an exercise you can now set up a simple hierarchy for order relations, including a class
for ordered commutative monoids, which have both a partial order and a commutative monoid structure
such that ``‚àÄ a b : Œ±, a ‚â§ b ‚Üí ‚àÄ c : Œ±, c * a ‚â§ c * b``. Of course you need to add fields and maybe
``extends`` clauses to the following classes.
BOTH: -/
-- QUOTE:

class LE‚ÇÅ (Œ± : Type) where
  /-- The Less-or-Equal relation. -/
  le : Œ± ‚Üí Œ± ‚Üí Prop

@[inherit_doc] infix:50 " ‚â§‚ÇÅ " => LE‚ÇÅ.le

class Preorder‚ÇÅ (Œ± : Type)
-- SOLUTIONS:
  extends LE‚ÇÅ Œ± where
  le_refl : ‚àÄ a : Œ±, a ‚â§‚ÇÅ a
  le_trans : ‚àÄ a b c : Œ±, a ‚â§‚ÇÅ b ‚Üí b ‚â§‚ÇÅ c ‚Üí a ‚â§‚ÇÅ c
-- BOTH:

class PartialOrder‚ÇÅ (Œ± : Type)
-- SOLUTIONS:
  extends Preorder‚ÇÅ Œ± where
  le_antisymm : ‚àÄ a b : Œ±, a ‚â§‚ÇÅ b ‚Üí b ‚â§‚ÇÅ a ‚Üí a = b
-- BOTH:

class OrderedCommMonoid‚ÇÅ (Œ± : Type)
-- SOLUTIONS:
  extends PartialOrder‚ÇÅ Œ±, CommMonoid‚ÇÉ Œ± where
  mul_of_le : ‚àÄ a b : Œ±, a ‚â§‚ÇÅ b ‚Üí ‚àÄ c : Œ±, c * a ‚â§‚ÇÅ c * b
-- BOTH:

instance : OrderedCommMonoid‚ÇÅ ‚Ñï where
-- SOLUTIONS:
  le := (¬∑ ‚â§ ¬∑)
  le_refl := fun _ ‚Ü¶ le_rfl
  le_trans := fun _ _ _ ‚Ü¶ le_trans
  le_antisymm := fun _ _ ‚Ü¶ le_antisymm
  mul := (¬∑ * ¬∑)
  mul_assoc‚ÇÉ := mul_assoc
  one := 1
  one_mul := one_mul
  mul_one := mul_one
  mul_comm := mul_comm
  mul_of_le := fun _ _ h c ‚Ü¶ Nat.mul_le_mul_left c h
-- QUOTE.
/- TEXT:



We now want to discuss algebraic structures involving several types. The prime example
is modules over rings. If you don't know what is a module, you can pretend it means vector space
and think that all our rings are fields. Those structures are commutative additive groups
equipped with a scalar multiplication by elements of some ring.

We first define the data-carrying type class of scalar multiplication by some type ``Œ±`` on some
type ``Œ≤``, and give it a right associative notation.
BOTH: -/

-- QUOTE:
class SMul‚ÇÉ (Œ± : Type) (Œ≤ : Type) where
  /-- Scalar multiplication -/
  smul : Œ± ‚Üí Œ≤ ‚Üí Œ≤

infixr:73 " ‚Ä¢ " => SMul‚ÇÉ.smul
-- QUOTE.

/- TEXT:
Then we can define modules (again think about vector spaces if you don't know what is a module).
BOTH: -/

-- QUOTE:
class Module‚ÇÅ (R : Type) [Ring‚ÇÉ R] (M : Type) [AddCommGroup‚ÇÉ M] extends SMul‚ÇÉ R M where
  zero_smul : ‚àÄ m : M, (0 : R) ‚Ä¢ m = 0
  one_smul : ‚àÄ m : M, (1 : R) ‚Ä¢ m = m
  mul_smul : ‚àÄ (a b : R) (m : M), (a * b) ‚Ä¢ m = a ‚Ä¢ b ‚Ä¢ m
  add_smul : ‚àÄ (a b : R) (m : M), (a + b) ‚Ä¢ m = a ‚Ä¢ m + b ‚Ä¢ m
  smul_add : ‚àÄ (a : R) (m n : M), a ‚Ä¢ (m + n) = a ‚Ä¢ m + a ‚Ä¢ n
-- QUOTE.

/- TEXT:
There is something interesting going on here. While it isn't too surprising that the
ring structure on ``R`` is a parameter in this definition, you probably expected ``AddCommGroup‚ÇÉ M``
to be part of the ``extends`` clause just as ``SMul‚ÇÉ R M`` is.  Trying to do that would lead
to a mysterious sounding error message:
``cannot find synthesization order for instance Module‚ÇÅ.toAddCommGroup‚ÇÉ with type (R : Type) ‚Üí [inst : Ring‚ÇÉ R] ‚Üí {M : Type} ‚Üí [self : Module‚ÇÅ R M] ‚Üí AddCommGroup‚ÇÉ M
all remaining arguments have metavariables: Ring‚ÇÉ ?R @Module‚ÇÅ ?R ?inst‚úù M``.
In order to understand this message, you need to remember that such an ``extends`` clause would
lead to a field ``Module‚ÇÉ.toAddCommGroup‚ÇÉ`` marked as an instance. This instance
would have the signature appearing in the error message:
``(R : Type) ‚Üí [inst : Ring‚ÇÉ R] ‚Üí {M : Type} ‚Üí [self : Module‚ÇÅ R M] ‚Üí AddCommGroup‚ÇÉ M``.
With such an instance in the type class database, each time Lean would look for a
``AddCommGroup‚ÇÉ M`` instance for some ``M``, it would need to go hunting for a completely
unspecified type ``R`` and a ``Ring‚ÇÉ R`` instance before embarking on the main quest of finding a
``Module‚ÇÅ R M`` instance. Those two side-quests are represented by the meta-variables mentioned in
the error message and denoted by ``?R`` and ``?inst‚úù`` there. Such a ``Module‚ÇÉ.toAddCommGroup‚ÇÉ``
instance would then be a huge trap for the instance resolution procedure and then ``class`` command
refuses to set it up.

What about ``extends SMul‚ÇÉ R M`` then? That one creates a field
``Module‚ÇÅ.toSMul‚ÇÉ : {R : Type} ‚Üí  [inst : Ring‚ÇÉ R] ‚Üí {M : Type} ‚Üí [inst_1 : AddCommGroup‚ÇÉ M] ‚Üí [self : Module‚ÇÅ R M] ‚Üí SMul‚ÇÉ R M``
whose end result ``SMul‚ÇÉ R M`` mentions both ``R`` and ``M`` so this field can
safely be used as an instance. The rule is easy to remember: each class appearing in the
``extends`` clause should mention every type appearing in the parameters.

Let us create our first module instance: a ring is a module over itself using its multiplication
as a scalar multiplication.
BOTH: -/
-- QUOTE:
instance selfModule (R : Type) [Ring‚ÇÉ R] : Module‚ÇÅ R R where
  smul := fun r s ‚Ü¶ r*s
  zero_smul := zero_mul
  one_smul := one_mul
  mul_smul := mul_assoc‚ÇÉ
  add_smul := Ring‚ÇÉ.right_distrib
  smul_add := Ring‚ÇÉ.left_distrib
-- QUOTE.
/- TEXT:
As a second example, every abelian group is a module over ``‚Ñ§`` (this is one of the reason to
generalize the theory of vector spaces by allowing non-invertible scalars). First one can define
scalar multiplication by a natural number for any type equipped with a zero and an addition:
``n ‚Ä¢ a`` is defined as ``a + ‚ãØ + a`` where ``a`` appears ``n`` times. Then this is extended
to scalar multiplication by an integer by ensuring ``(-1) ‚Ä¢ a = -a``.
BOTH: -/
-- QUOTE:

def nsmul‚ÇÅ [Zero M] [Add M] : ‚Ñï ‚Üí M ‚Üí M
  | 0, _ => 0
  | n + 1, a => a + nsmul‚ÇÅ n a

def zsmul‚ÇÅ {M : Type*} [Zero M] [Add M] [Neg M] : ‚Ñ§ ‚Üí M ‚Üí M
  | Int.ofNat n, a => nsmul‚ÇÅ n a
  | Int.negSucc n, a => -nsmul‚ÇÅ n.succ a
-- QUOTE.
/- TEXT:
Proving this gives rise to a module structure is a bit tedious and not interesting for the
current discussion, so we will sorry all axioms. You are *not* asked to replace those sorries
with proofs. If you insist on doing it then you will probably want to state and prove several
intermediate lemmas about ``nsmul‚ÇÅ`` and ``zsmul‚ÇÅ``.
BOTH: -/
-- QUOTE:

instance abGrpModule (A : Type) [AddCommGroup‚ÇÉ A] : Module‚ÇÅ ‚Ñ§ A where
  smul := zsmul‚ÇÅ
  zero_smul := sorry
  one_smul := sorry
  mul_smul := sorry
  add_smul := sorry
  smul_add := sorry
-- QUOTE.
/- TEXT:
A much more important issue is that we now have two module structures over the ring ``‚Ñ§``
for ``‚Ñ§`` itself: ``abGrpModule ‚Ñ§`` since ``‚Ñ§`` is a abelian group, and ``selfModule ‚Ñ§`` since
``‚Ñ§`` is a ring. Those two module structure correspond to the same abelian group structure,
but it is not obvious that they have the same scalar multiplication. They actually do, but
this isn't true by definition, it requires a proof. This is very bad news for the type class
instance resolution procedure and will lead to very frustrating failures for users of this
hierarchy. When directly asked to find an instance, Lean will pick one, and we can see
which one using:
BOTH: -/
-- QUOTE:

#synth Module‚ÇÅ ‚Ñ§ ‚Ñ§ -- abGrpModule ‚Ñ§

-- QUOTE.
/- TEXT:
But in a more indirect context it can happen that Lean infers the one and then gets confused.
This situation is known as a bad diamond. This has nothing to do with the diamond operation
we used above, it refers to the way one can draw the paths from ``‚Ñ§`` to its ``Module‚ÇÅ ‚Ñ§``
going through either ``AddCommGroup‚ÇÉ ‚Ñ§`` or ``Ring‚ÇÉ ‚Ñ§``.

It is important to understand that not all diamonds are bad. In fact there are diamonds everywhere
in mathlib, and also in this chapter. Already at the very beginning we saw one can go
from ``Monoid‚ÇÅ Œ±`` to ``Dia‚ÇÅ Œ±`` through either ``Semigroup‚ÇÅ Œ±`` or ``DiaOneClass‚ÇÅ Œ±`` and
thanks to the work done by the ``class`` command, the resulting two ``Dia‚ÇÅ Œ±`` instances
are definitionally equal. In particular a diamond having a ``Prop``-valued class at the bottom
cannot be bad since any too proofs of the same statement are definitionally equal.

But the diamond we created with modules is definitely bad. The offending piece is the ``smul``
field which is data, not a proof, and we have two constructions that are not definitionally equal.
The robust way of fixing this issue is to make sure that going from a rich structure to a
poor structure is always done by forgetting data, not by defining data. This well-known pattern
as been named "forgetful inheritance" and extensively discussed in
https://inria.hal.science/hal-02463336.

In our concrete case, we can modify the definition of ``AddMonoid‚ÇÉ`` to include a ``nsmul`` data
field and some ``Prop``-valued fields ensuring this operation is provably the one we constructed
above. Those fields are given default values using ``:=`` after their type in the definition below.
Thanks to these default values, most instances would be constructed exactly as with our previous
definitions. But in the special case of ``‚Ñ§`` we will be able to provide specific values.
BOTH: -/
-- QUOTE:

class AddMonoid‚ÇÑ (M : Type) extends AddSemigroup‚ÇÉ M, AddZeroClass M where
  /-- Multiplication by a natural number. -/
  nsmul : ‚Ñï ‚Üí M ‚Üí M := nsmul‚ÇÅ
  /-- Multiplication by `(0 : ‚Ñï)` gives `0`. -/
  nsmul_zero : ‚àÄ x, nsmul 0 x = 0 := by intros; rfl
  /-- Multiplication by `(n + 1 : ‚Ñï)` behaves as expected. -/
  nsmul_succ : ‚àÄ (n : ‚Ñï) (x), nsmul (n + 1) x = x + nsmul n x := by intros; rfl

instance mySMul {M : Type} [AddMonoid‚ÇÑ M] : SMul ‚Ñï M := ‚ü®AddMonoid‚ÇÑ.nsmul‚ü©
-- QUOTE.
/- TEXT:

Let us check we can still construct a product monoid instance without providing the ``nsmul``
related fields.
BOTH: -/
-- QUOTE:

instance (M N : Type) [AddMonoid‚ÇÑ M] [AddMonoid‚ÇÑ N] : AddMonoid‚ÇÑ (M √ó N) where
  add := fun p q ‚Ü¶ (p.1 + q.1, p.2 + q.2)
  add_assoc‚ÇÉ := fun a b c ‚Ü¶ by ext <;> apply add_assoc‚ÇÉ
  zero := (0, 0)
  zero_add := fun a ‚Ü¶ by ext <;> apply zero_add
  add_zero := fun a ‚Ü¶ by ext <;> apply add_zero
-- QUOTE.
/- TEXT:
And now let us handle the special case of ``‚Ñ§`` where we want to build ``nsmul`` using the coercion
of ``‚Ñï`` to ``‚Ñ§`` and the multiplication on ``‚Ñ§``. Note in particular how the proof fields
contain more work than in the default value above.
BOTH: -/
-- QUOTE:

instance : AddMonoid‚ÇÑ ‚Ñ§ where
  add := (¬∑ + ¬∑)
  add_assoc‚ÇÉ := Int.add_assoc
  zero := 0
  zero_add := Int.zero_add
  add_zero := Int.add_zero
  nsmul := fun n m ‚Ü¶ (n : ‚Ñ§) * m
  nsmul_zero := Int.zero_mul
  nsmul_succ := fun n m ‚Ü¶ show (n + 1 : ‚Ñ§) * m = m + n * m
    by rw [Int.add_mul, Int.add_comm, Int.one_mul]
-- QUOTE.
/- TEXT:
Let us check we solved our issue. Because Lean already has a definition of scalar multiplication
of a natural number and an integer, and we want to make sure our instance is used, we won't use
the ``‚Ä¢`` notation but call ``SMul.mul`` and explicitly provide our instance defined above.
BOTH: -/
-- QUOTE:

example (n : ‚Ñï) (m : ‚Ñ§) : SMul.smul (self := mySMul) n m = n * m := rfl
-- QUOTE.
/- TEXT:
This story then continues with incorporating a ``zsmul`` field into the definition of groups
and similar tricks. You are now ready to read the definition of monoids, groups, rings and modules
in mathlib. There are more complicated than what we have seen here, because they are part of a huge
hierarchy, but all principles have been explained above.

As an exercise, you can come back to the order relation hierarchy you built above and try
to incorporate a type class ``LT‚ÇÅ`` carrying the Less-Than notation ``<‚ÇÅ`` and make sure
that every preorder comes with a ``<‚ÇÅ`` which has a default value built from ``‚â§‚ÇÅ`` and a
``Prop``-valued field asserting the natural relation between those two comparison operators.
-/

-- SOLUTIONS:

class LT‚ÇÅ (Œ± : Type) where
  /-- The Less-Than relation -/
  lt : Œ± ‚Üí Œ± ‚Üí Prop

@[inherit_doc] infix:50 " <‚ÇÅ " => LT‚ÇÅ.lt

class PreOrder‚ÇÇ (Œ± : Type) extends LE‚ÇÅ Œ±, LT‚ÇÅ Œ± where
  le_refl : ‚àÄ a : Œ±, a ‚â§‚ÇÅ a
  le_trans : ‚àÄ a b c : Œ±, a ‚â§‚ÇÅ b ‚Üí b ‚â§‚ÇÅ c ‚Üí a ‚â§‚ÇÅ c
  lt := fun a b ‚Ü¶ a ‚â§‚ÇÅ b ‚àß ¬¨b ‚â§‚ÇÅ a
  lt_iff_le_not_le : ‚àÄ a b : Œ±, a <‚ÇÅ b ‚Üî a ‚â§‚ÇÅ b ‚àß ¬¨b ‚â§‚ÇÅ a := by intros; rfl
