import analysis.normed_space.finite_dimension
import analysis.convolution
import measure_theory.function.jacobian
import measure_theory.integral.bochner
import measure_theory.measure.lebesgue

open set filter
open_locale topological_space filter ennreal
noncomputable theory

/- TEXT:
.. index:: measure theory

.. _measure_theory:

Measure theory
--------------

The general context for integration in mathlib is measure theory. Even the elementary
integrals of the previous section are secretely Bochner integrals. Bochner integration is
a generalization of Lebesgue integration where the target space can be any Banach space,
not necessarily finite dimensional.

The first layer is the notion of a :math:`\sigma`-algebra of sets that are "measurable". 
Equipping a type with such a structure is the role of the type class ``measurable_space``.
The empty and univ sets are measurable, the complement of a measurable set is measurable, 
and a countable union or intersection of measurable sets is measurable (of course these axioms are
redundant). In this context the countability assumption is spelled using ``encodable``.
TEXT. -/

-- QUOTE:
variables {Î± : Type*} [measurable_space Î±]


example : measurable_set (âˆ… : set Î±) := measurable_set.empty 

example : measurable_set (univ : set Î±) := measurable_set.univ

example {s : set Î±} (hs : measurable_set s) : measurable_set sá¶œ :=
hs.compl

example : encodable â„• := 
by apply_instance

example (n : â„•) : encodable (fin n) := 
by apply_instance

variables {Î¹ : Type*} [encodable Î¹]

example {f : Î¹ â†’ set Î±} (h : âˆ€ b, measurable_set (f b)) :
  measurable_set (â‹ƒ b, f b) := 
measurable_set.Union h

example {f : Î¹ â†’ set Î±} (h : âˆ€ b, measurable_set (f b)) :
  measurable_set (â‹‚ b, f b) := 
measurable_set.Inter h

-- QUOTE.

/- TEXT: 
Once a type is measurable we can measure it. On paper, a measure on a type equipped with a
:math:`\sigma`-algebra is a function from measurable sets to extended non-negative reals that is 
additive on countable disjoint unions. In mathlib we don't want to carry around measurability assumptions
each time we write an application of the measure to a set. So we extend the measure to any set ``s``
as the infimum of measures of measurable sets containing ``s``. Of course many lemmas still require
measurability assumptions, but not all.

TEXT. -/

-- QUOTE:
open measure_theory

variables {Î¼ : measure Î±}

example (s : set Î±) : Î¼ s = â¨… t (st : s âŠ† t) (ht : measurable_set t), Î¼ t := 
measure_eq_infi s

example  (s : Î¹ â†’ set Î±) : Î¼ (â‹ƒ i, s i) â‰¤ âˆ‘' i, Î¼ (s i) :=
measure_Union_le s

example {f : â„• â†’ set Î±} (hmeas : âˆ€ i, measurable_set (f i)) (hdis : pairwise (disjoint on f)) :
  Î¼ (â‹ƒ i, f i) = âˆ‘' i, Î¼ (f i) :=
Î¼.m_Union hmeas hdis
-- QUOTE.

/- TEXT:
Measures come with a notion of properties holding almost everywhere. This is of course a special case of what
filters do, but we have a special notation for this.
TEXT. -/

-- QUOTE:
example {P : Î± â†’ Prop} : (âˆ€áµ x âˆ‚Î¼, P x) â†” âˆ€á¶  x in Î¼.ae, P x :=
iff.rfl
-- QUOTE.

/- TEXT:
Integration
-----------

Now that we have measurable spaces and measures we can consider integrals. As explained above, the very general
integration that we use allow any Banach space as the target. As usual we don't want our notation to 
carry around assumptions so we can always write an integral and it will be zero if the function is not integrable.
And of course most lemmas do have integrability assumptions.


TEXT. -/

-- QUOTE:
section

variables {E : Type*} [normed_group E] [normed_space â„ E] [complete_space E]
  {f : Î± â†’ E}

example {f g : Î± â†’ E} (hf : integrable f Î¼) (hg : integrable g Î¼) : 
  âˆ« a, f a + g a âˆ‚Î¼ = âˆ« a, f a âˆ‚Î¼ + âˆ« a, g a âˆ‚Î¼ :=
integral_add hf hg

-- QUOTE.

/- TEXT:
As an example of a complicated interaction between our various conventions, let us see how to integrate constant functions.
Recall that ``Î¼`` takes values in ``â„â‰¥0âˆ``, the type of extended non-negative reals. There is a function
``ennreal.to_real : â„â‰¥0âˆ â†’ â„`` which sends ``âŠ¤``, the point at infinity, to zero. 
For any ``s : set Î±``, if ``Î¼ s = âŠ¤`` then constant functions are not integrable so their integral
is zero by definition, and so is ``(Î¼ s).to_real``. So in all cases we have the following lemma.
TEXT. -/


-- QUOTE:

example {s : set Î±} (c : E) :
  âˆ« x in s, c âˆ‚Î¼ = (Î¼ s).to_real â€¢ c :=
set_integral_const c

-- QUOTE.

/- TEXT:
We now quickly review how to access the most important theorems in integration theory, starting
with the dominated convergence theorem. There are several versions, we only show the most basic one.
TEXT. -/

-- QUOTE:
example {F : â„• â†’ Î± â†’ E} {f : Î± â†’ E} (bound : Î± â†’ â„)
  (hmeas : âˆ€ n, ae_strongly_measurable (F n) Î¼) 
  (hint : integrable bound Î¼)
  (hbound : âˆ€ n, âˆ€áµ a âˆ‚Î¼, âˆ¥F n aâˆ¥ â‰¤ bound a)
  (hlim : âˆ€áµ a âˆ‚Î¼, tendsto (Î» (n : â„•), F n a) at_top (ğ“ (f a))) :
  tendsto (Î» n, âˆ« a, F n a âˆ‚Î¼) at_top (ğ“ (âˆ« a, f a âˆ‚Î¼)) :=
tendsto_integral_of_dominated_convergence bound hmeas hint hbound hlim

-- QUOTE.

/- TEXT:
Then we have Fubini's theorem for integrals on product type.

TEXT. -/

-- QUOTE:
example
  {Î± : Type*} [measurable_space Î±] 
  {Î¼ : measure Î±} [sigma_finite Î¼] 
  {Î² : Type*} [measurable_space Î²] {Î½ : measure Î²} [sigma_finite Î½]  
  (f : Î± Ã— Î² â†’ E) (hf : integrable f (Î¼.prod Î½)) :
  âˆ« z, f z âˆ‚Î¼.prod Î½ = âˆ« x, âˆ« y, f (x, y) âˆ‚Î½ âˆ‚Î¼ :=
integral_prod f hf

-- QUOTE.
end

/- TEXT:
There is a very general version of convolution using any continuous bilinear form.

TEXT. -/

section
-- QUOTE:
open_locale convolution

variables {ğ•œ : Type*} {G : Type*} {E : Type*} {E' : Type*} {F : Type*} [normed_group E]
  [normed_group E'] [normed_group F] [nondiscrete_normed_field ğ•œ]
  [normed_space ğ•œ E] [normed_space ğ•œ E'] [normed_space ğ•œ F]
  [measurable_space G] [normed_space â„ F] [complete_space F] [has_sub G]

example (f : G â†’ E) (g : G â†’ E') (L : E â†’L[ğ•œ] E' â†’L[ğ•œ] F) (Î¼ : measure G) : 
  f â‹†[L, Î¼] g = Î» x, âˆ« t, L (f t) (g (x - t)) âˆ‚Î¼ := 
rfl

-- QUOTE.

end
/- TEXT:
And finally we have a very general version of the change of variables formula. In the statement below,
``borel_space E`` means the :math:`\sigma`-algebra on ``E`` is generated by open sets.
And ``is_add_haar_measure Î¼`` means ``Î¼`` is left-invariant and gives
finite mass to compact sets and positive mass to open sets.
TEXT. -/

-- QUOTE:


example {E : Type*} [normed_group E] [normed_space â„ E] [finite_dimensional â„ E]  
  [measurable_space E] [borel_space E] (Î¼ : measure E) [Î¼.is_add_haar_measure]
  {F : Type*}[normed_group F] [normed_space â„ F] [complete_space F]
  {s : set E} {f : E â†’ E} {f' : E â†’ (E â†’L[â„] E)}  
  (hs : measurable_set s) 
  (hf : âˆ€ (x : E), x âˆˆ s â†’ has_fderiv_within_at f (f' x) s x) 
  (h_inj : inj_on f s)
  (g : E â†’ F) : 
  âˆ« x in f '' s, g x âˆ‚Î¼ = âˆ« x in s, |(f' x).det| â€¢ g (f x) âˆ‚Î¼ :=
integral_image_eq_integral_abs_det_fderiv_smul Î¼ hs hf h_inj g

-- QUOTE.
