import topology.instances.real
import analysis.normed_space.banach_steinhaus

open set filter
open_locale topological_space filter

/- TEXT:
.. index:: metric space

.. _metric_spaces:

Metric spaces
--------------

Examples in the previous section focus on sequences of real numbers. In this section we will go up a bit in generality and focus on 
metric spaces. A metric space is a type ``X`` equipped with a distance function ``dist : X ‚Üí X ‚Üí ‚Ñù`` which is a generalization of
the function ``Œª x y, |x - y|`` from the case where ``X = ‚Ñù``.

Introducing such a space is easy and we will check all properties required from the distance function.
EXAMPLES: -/
-- QUOTE:

variables {X : Type*} [metric_space X] (a b c : X)

#check (dist a b : ‚Ñù)

#check (dist_nonneg : 0 ‚â§ dist a b)

#check (dist_eq_zero : dist a b  = 0 ‚Üî a = b)

#check (dist_comm a b : dist a b  = dist b a)

#check (dist_triangle a b c : dist a c ‚â§ dist a b + dist b c)
-- QUOTE.

/- TEXT:
Note we also have variants where the distance can be infinite or where ``dist a b`` can be zero without having ``a = b`` or both.
They are called ``emetric_space``, ``pseudo_metric_space`` and ``pseudo_emetric_space`` respectively (here "e" stands for "extended").

TEXT. -/

-- Note the next three lines are not quoted, their purpose is to make sure those things don't get renamed while we're looking elsewhere.
#check emetric_space
#check pseudo_metric_space
#check pseudo_emetric_space


/- TEXT:
Note that our journey from ``‚Ñù`` to metric spaces jumped over the special case of normed spaces that also require linear algebra and
will be explained as part of the calculus chapter.

Convergence and continuity
^^^^^^^^^^^^^^^^^^^^^^^^^^

Using distance functions, we can already define convergent sequences and continuous functions between metric spaces. 
They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition is terms of distances.
EXAMPLES: -/
-- QUOTE:

example {u : ‚Ñï ‚Üí X} {a : X} : 
  tendsto u at_top (ùìù a) ‚Üî ‚àÄ Œµ > 0, ‚àÉ N, ‚àÄ n ‚â• N, dist (u n) a < Œµ :=
metric.tendsto_at_top

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} :
  continuous f ‚Üî
  ‚àÄ x : X, ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ x', dist x' x < Œ¥ ‚Üí dist (f x') (f x) < Œµ :=
metric.continuous_iff

-- QUOTE.

/- TEXT:
.. index:: continuity, tactics ; continuity


A *lot* of lemmas have some continuity assumptions, no we end up proving a lot of continuity results and there 
is a ``continuity`` tactic devoted to this task. Let's prove a continuity statement that will be needed
in an exercise below. Notice that Lean knows how to treat a product of two metric spaces as a metric space, so
it makes sense to consider continuous functions from ``X √ó X`` to ``‚Ñù``. 
In particular the (uncurried version of the) distance function is such a function.

TEXT. -/

-- QUOTE:
example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} (hf : continuous f) : 
  continuous (Œª p : X √ó X, dist (f p.1) (f p.2)) :=
by continuity
-- QUOTE.

/- TEXT:
This tactic is a bit slow, so it is also useful to know
how to do it by hand. We first need to use that ``Œª p : X √ó X, f p.1`` is continuous because it
is the composition of ``f``, which is continuous by assumption ``hf``, and the projection ``prod.fst`` whose continuity
is the content of the lemma ``continuous_fst``. The composition property is ``continuous.comp`` which is
in the ``continuous`` namespace so we can use dot notation to compress
``continuous.comp hf continuous_fst`` into ``hf.comp continuous_fst`` which is actually more readable
since it really reads as composing our assumption and our lemma. 
We can do the same for the second component to get continuity of ``Œª p : X √ó X, f p.2``. We then assemble
those two continuities using ``continuous.prod_mk`` to get
``(hf.comp continuous_fst).prod_mk (hf.comp continuous_snd) : continuous (Œª p : X √ó X, (f p.1, f p.2))``
and compose once more to get our full proof.
EXAMPLES: -/
-- QUOTE:

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} (hf : continuous f) : 
  continuous (Œª p : X √ó X, dist (f p.1) (f p.2)) :=
continuous_dist.comp ((hf.comp continuous_fst).prod_mk (hf.comp continuous_snd))

-- QUOTE.

/- TEXT:
The combination of ``continuous.prod_mk`` and ``continuous_dist`` via ``continuous.comp`` feels clunky,
even when heavily using dot notation as above. A more serious issue is that this nice proof requires a lot of
planning. Lean accepts the above proof term because it is a full term proving a statement which is 
definitionally equivalent to our goal, the crucial definition to unfold being that of a composition of functions. 
Indeed our target function ``Œª p : X √ó X, dist (f p.1) (f p.2)`` is not presented as a composition. 
The proof term we provided proves continuity of ``dist ‚àò (Œª p : X √ó X, (f p.1, f p.2))`` which happens 
to be definitionally equal to our target function. But if we try to build this proof gradually using 
tactics starting with ``apply continuous_dist.comp`` then Lean's elaborator will fail to recognize a 
composition and refuse to apply this lemma. It is especially bad at this when products of types are involved.

A better lemma to apply here is 
``continuous.dist {f g : X ‚Üí Y} : continuous f ‚Üí continuous g ‚Üí continuous (Œª x, dist (f x) (g x))``
which is nicer to Lean's elaborator and also provides a shorter proof when directly providing a full 
proof term, as can be seen from the following two new proofs of the above statement:
TEXT. -/

-- QUOTE:

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} (hf : continuous f) : 
  continuous (Œª p : X √ó X, dist (f p.1) (f p.2)) :=
begin
  apply continuous.dist,
  exact hf.comp continuous_fst,
  exact hf.comp continuous_snd
end

example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} (hf : continuous f) : 
  continuous (Œª p : X √ó X, dist (f p.1) (f p.2)) :=
(hf.comp continuous_fst).dist (hf.comp continuous_snd)

-- QUOTE.

/- TEXT:
Note that, without the elaboration issue coming from composition, another way to compress
our proof would be to use ``continuous.prod_map`` which is sometimes useful and gives
as an alternate proof term ``continuous_dist.comp (hf.prod_map hf)`` which even shorter to type.

Since it is sad to decide between a version which is better for elaboration and a version which is shorter
to type, let us wrap this discussion with a last bit of compression offered
by ``continuous.fst'`` which allows to compress ``hf.comp continuous_fst`` to ``hf.fst'`` (and the same with ``snd``) 
and get our final proof, now bordering obfuscation.

TEXT. -/

-- QUOTE:
example {X Y : Type*} [metric_space X] [metric_space Y] {f : X ‚Üí Y} (hf : continuous f) : 
  continuous (Œª p : X √ó X, dist (f p.1) (f p.2)) :=
hf.fst'.dist hf.snd'

-- QUOTE.

/- TEXT:
It's your turn now to prove some continuity lemma. After trying the continuity tactic, you will need 
``continuous.add``, ``continuous_pow`` and ``continuous_id`` to do it by hand.

TEXT. -/

-- QUOTE:
example {f : ‚Ñù ‚Üí X} (hf : continuous f) : continuous (Œª x : ‚Ñù, f (x^2 + x)) :=
sorry

-- QUOTE.

-- SOLUTIONS:
example {f : ‚Ñù ‚Üí X} (hf : continuous f) : continuous (Œª x : ‚Ñù, f (x^2 + x)) :=
hf.comp $ (continuous_pow 2).add continuous_id


/- TEXT:
So far we saw continuity as a global notion, but one can also define continuity at a point. 
TEXT. -/

-- QUOTE:

example {X Y : Type*} [metric_space X] [metric_space Y] (f : X ‚Üí Y) (a : X) : 
continuous_at f a ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {x}, dist x a < Œ¥ ‚Üí dist (f x) (f a) < Œµ :=
metric.continuous_at_iff

-- QUOTE.

/- TEXT:

Balls, open sets and closed sets
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

Once we have a distance function, the most important geometric definitions are (open) balls and closed balls.

EXAMPLES: -/
-- QUOTE:

variables r : ‚Ñù

example : metric.ball a r = {b | dist b a < r} := rfl

example : metric.closed_ball a r = {b | dist b a ‚â§ r} := rfl

-- QUOTE.

/- TEXT:
Note that `r` is any real number here, there is no sign restriction. Of course some statements do require a radius condition.
EXAMPLES: -/
-- QUOTE:

example (hr : 0 < r) : a ‚àà metric.ball a r := metric.mem_ball_self hr

example (hr : 0 ‚â§ r) : a ‚àà metric.closed_ball a r := metric.mem_closed_ball_self hr

-- QUOTE.
/- TEXT:
Once we have balls, we can define open sets. They are actually defined in a more general setting covered in the next section,
but we have lemmas recasting the definition is terms of balls.

TEXT. -/

-- QUOTE:
example (s : set X) : is_open s ‚Üî ‚àÄ x ‚àà s, ‚àÉ Œµ > 0, metric.ball x Œµ ‚äÜ s :=
metric.is_open_iff

-- QUOTE.

/- TEXT:
Then closed sets are sets whose complement is open. Their important property is they are closed under limits. The closure of a set is the smallest subset containing it.
TEXT. -/
-- QUOTE:

example {s : set X} : is_closed s ‚Üî is_open s·∂ú :=
is_open_compl_iff.symm

example {s : set X} (hs : is_closed s) {u : ‚Ñï ‚Üí X} (hu : tendsto u at_top (ùìù a)) 
  (hus : ‚àÄ n, u n ‚àà s) : a ‚àà s :=
hs.mem_of_tendsto hu (eventually_of_forall hus)

example {s : set X} : a ‚àà closure s ‚Üî ‚àÄ Œµ > 0, ‚àÉ b ‚àà s, a ‚àà metric.ball b Œµ :=
metric.mem_closure_iff
-- QUOTE.

/- TEXT:
Do the next exercise without using `mem_closure_iff_seq_limit`
TEXT. -/

-- QUOTE:
example {u : ‚Ñï ‚Üí X} (hu : tendsto u at_top (ùìù a)) {s : set X} (hs : ‚àÄ n, u n ‚àà s) : 
  a ‚àà closure s :=
sorry

-- QUOTE.

-- SOLUTIONS:
example {u : ‚Ñï ‚Üí X} (hu : tendsto u at_top (ùìù a)) {s : set X} (hs : ‚àÄ n, u n ‚àà s) : 
  a ‚àà closure s :=
begin
  rw metric.tendsto_at_top at hu,
  rw metric.mem_closure_iff,
  intros Œµ Œµ_pos,
  rcases hu Œµ Œµ_pos with ‚ü®N, hN‚ü©,
  refine ‚ü®u N, hs _, _‚ü©,
  rw dist_comm,
  exact hN N le_rfl
end


/- TEXT:

Remember from the filters sections that neighborhood filters play a big role in mathlib.
In the metric space context, the crucial point is that balls provide bases for those filters.
The main lemmas here are ``metric.nhds_basis_ball`` and ``metric.nhds_basis_closed_ball`` 
that claim this for open and closed balls with positive radius. The center point is an implicit
argument so we can invoke ``filter.has_basis.mem_iff`` as in the following example.

TEXT. -/

-- QUOTE:
example {x : X} {s : set X} : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, metric.ball x Œµ ‚äÜ s :=
metric.nhds_basis_ball.mem_iff

example {x : X} {s : set X} : s ‚àà ùìù x ‚Üî ‚àÉ Œµ > 0, metric.closed_ball x Œµ ‚äÜ s :=
metric.nhds_basis_closed_ball.mem_iff

-- QUOTE.

/- TEXT:

Compactness
^^^^^^^^^^^

Compactness is an important topological notion. It distinguishes subsets of a metric space
that enjoy the same kind of properties as segments in reals compared to other intervals:

* Any sequence taking value in a compact set has a subsequence that converges in this set
* Any continuous function on a nonempty compact set with values in real numbers is bounded and achieves its bounds somewhere (this is called the extreme values theorem).
* Compact sets are closed sets.

Let us first check that the unit interval in reals is indeed a compact set, and then check the above
claims for compact sets in general metric spaces. In the second statement we only 
need continuity on the given set so we will use ``continuous_on`` instead of ``continuous``, and
we will give separate statements for the minimum and the maximum. Of course all these results
are deduced from more general versions, some of which will be discussed in later sections.

TEXT. -/

-- QUOTE:

example : is_compact (set.Icc 0 1 : set ‚Ñù) :=
is_compact_Icc

example {s : set X} (hs : is_compact s) {u : ‚Ñï ‚Üí X} (hu : ‚àÄ n, u n ‚àà s) :
  ‚àÉ a ‚àà s, ‚àÉ œÜ : ‚Ñï ‚Üí ‚Ñï, strict_mono œÜ ‚àß tendsto (u ‚àò œÜ) at_top (ùìù a) :=
hs.tendsto_subseq hu

example {s : set X} (hs : is_compact s) (hs' : s.nonempty) 
  {f : X ‚Üí ‚Ñù} (hfs : continuous_on f s) :
  ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f x ‚â§ f y :=
hs.exists_forall_le hs' hfs

example {s : set X} (hs : is_compact s) (hs' : s.nonempty) 
  {f : X ‚Üí ‚Ñù} (hfs : continuous_on f s) :
  ‚àÉ x ‚àà s, ‚àÄ y ‚àà s, f y ‚â§ f x :=
hs.exists_forall_ge hs' hfs

example {s : set X} (hs : is_compact s) : is_closed s :=
hs.is_closed

-- QUOTE.

/- TEXT:  

We can also metric spaces which are globally compact, using an extra ``Prop``-valued type class:

TEXT. -/

-- QUOTE:
example {X : Type*} [metric_space X] [compact_space X] : is_compact (univ : set X) :=
compact_univ
-- QUOTE.

/- TEXT:

In a compact metric space any closed set is compact, this is ``is_compact.is_closed``.

TEXT. -/

#check is_compact.is_closed

/- TEXT:
Uniformly continuous functions
^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

We now turn to uniformity notions on metric spaces : uniformly continuous functions, Cauchy sequences and completeness.
Again those are defined in a more general context but we have lemmas in the metric name space to access their elementary definitions.
We start with uniform continuity.

TEXT. -/

-- QUOTE:
example {X : Type*} [metric_space X] {Y : Type*} [metric_space Y] {f : X ‚Üí Y} : 
  uniform_continuous f ‚Üî ‚àÄ Œµ > 0, ‚àÉ Œ¥ > 0, ‚àÄ {a b : X}, dist a b < Œ¥ ‚Üí dist (f a) (f b) < Œµ :=
metric.uniform_continuous_iff

-- QUOTE.

/- TEXT:
In order to practice manipulating all those definitions, we will prove that continuous
functions from a compact metric space to a metric space are uniformly continuous 
(we will see a more general version in a later section).

We will first give an informal sketch. Let ``f : X ‚Üí Y`` be a continuous function from
a compact metric space to a metric space.
We fix ``Œµ > 0`` and start looking for some ``Œ¥``.

Let ``œÜ : X √ó X ‚Üí ‚Ñù := Œª p, dist (f p.1) (f p.2)`` and let ``K := { p : X √ó X | Œµ ‚â§ œÜ p }``.
Observe ``œÜ`` is continuous since ``f`` and distance are continuous.
And ``K`` is clearly closed (use ``is_closed_le``) hence compact since ``X`` is compact.

Then we discuss two possibilities using ``eq_empty_or_nonempty``.
If ``K`` is empty then we are clearly done (we can set ``Œ¥ = 1`` for instance).
So let's assume ``K`` is not empty, and use the extreme value theorem to choose ``(x‚ÇÄ, x‚ÇÅ)`` attaining the infimum
of ``œÜ`` on ``K``. We can then set ``Œ¥ = dist x‚ÇÄ x‚ÇÅ`` and check everything works.

TEXT. -/

-- QUOTE:
example {X : Type*} [metric_space X] [compact_space X] {Y : Type*} [metric_space Y]
  {f : X ‚Üí Y} (hf : continuous f) : uniform_continuous f :=
sorry

-- QUOTE.

-- SOLUTIONs:
example {X : Type*} [metric_space X] [compact_space X] {Y : Type*} [metric_space Y]
  {f : X ‚Üí Y} (hf : continuous f) : uniform_continuous f :=
begin
  rw metric.uniform_continuous_iff,
  intros Œµ Œµ_pos,
  let œÜ : X √ó X ‚Üí ‚Ñù := Œª p, dist (f p.1) (f p.2),
  have œÜ_cont : continuous œÜ := hf.fst'.dist hf.snd',
  let K := { p : X √ó X | Œµ ‚â§ œÜ p },
  have K_closed : is_closed K := is_closed_le continuous_const œÜ_cont,
  have K_cpct : is_compact K := K_closed.is_compact,
  cases eq_empty_or_nonempty K with hK hK,
  { use [1, by norm_num],
    intros x y hxy,
    have : (x, y) ‚àâ K, by simp [hK],
    simpa [K] },
  { rcases K_cpct.exists_forall_le hK continuous_dist.continuous_on with ‚ü®‚ü®x‚ÇÄ, x‚ÇÅ‚ü©, xx_in, H‚ü©,
    use dist x‚ÇÄ x‚ÇÅ,
    split,
    { change _ < _,
      rw dist_pos,
      intro h,
      have : Œµ ‚â§ 0, by simpa [*] using xx_in,
      linarith },
    { intros x x',
      contrapose!,
      intros hxx',
      exact H (x, x') hxx' } },
end

/- TEXT:
Completeness
^^^^^^^^^^^^

A Cauchy sequence in a metric space is a sequence whose terms get closer and closer to each other. 
There are a couple of equivalent ways to state that idea.
In particular converging sequences are Cauchy. The converse is true only in so-called *complete* 
spaces.


TEXT. -/

-- QUOTE:

example (u : ‚Ñï ‚Üí X) : cauchy_seq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N : ‚Ñï, ‚àÄ m ‚â• N,  ‚àÄ n ‚â• N, dist (u m) (u n) < Œµ :=
metric.cauchy_seq_iff

example (u : ‚Ñï ‚Üí X) : cauchy_seq u ‚Üî ‚àÄ Œµ > 0, ‚àÉ N : ‚Ñï, ‚àÄ n ‚â• N, dist (u n) (u N) < Œµ :=
metric.cauchy_seq_iff'


example [complete_space X] (u : ‚Ñï ‚Üí X) (hu : cauchy_seq u) : ‚àÉ x, tendsto u at_top (ùìù x) :=
cauchy_seq_tendsto_of_complete hu

-- QUOTE.

/- TEXT:

We'll practice using this definition by proving a convenient criterion which is a special case of a
criterion appearing in mathlib. This is also a good opportunity to practive using big sums in
a geometric context. In addition to the explanations from the filters section, you will probably need 
``tendsto_pow_at_top_nhds_0_of_lt_1``, ``tendsto.mul`` and ``dist_le_range_sum_dist``.
TEXT. -/

open_locale big_operators
open finset

-- QUOTE:

lemma cauchy_seq_of_le_geometric_two' {u : ‚Ñï ‚Üí X} (hu : ‚àÄ (n : ‚Ñï), dist (u n) (u (n + 1)) ‚â§ (1 / 2) ^ n) :
  cauchy_seq u :=
begin
  rw metric.cauchy_seq_iff',  
  intros Œµ Œµ_pos,
  obtain ‚ü®N, hN‚ü© : ‚àÉ N : ‚Ñï, 1 / 2 ^ N * 2 < Œµ,
  { sorry },
  use N,
  intros n hn,
  obtain ‚ü®k, rfl : n = N + k‚ü© := le_iff_exists_add.mp hn,
  calc dist (u (N + k)) (u N) = dist (u (N+0)) (u (N + k)) : sorry
  ... ‚â§ ‚àë i in range k, dist (u (N + i)) (u (N + (i + 1))) : sorry
  ... ‚â§ ‚àë i in range k, (1/2 : ‚Ñù)^(N+i) : sorry
  ... = 1/2^N*‚àë i in range k, (1 / 2) ^ i : sorry
  ... ‚â§ 1/2^N*2 : sorry
  ... < Œµ : sorry
end
-- QUOTE.

-- SOLUTIONS:
example {u : ‚Ñï ‚Üí X} (hu : ‚àÄ (n : ‚Ñï), dist (u n) (u (n + 1)) ‚â§ (1 / 2) ^ n) :
  cauchy_seq u :=
begin
  rw metric.cauchy_seq_iff',  
  intros Œµ Œµ_pos,
  obtain ‚ü®N, hN‚ü© : ‚àÉ N : ‚Ñï, 1 / 2 ^ N * 2 < Œµ,
  { have : tendsto (Œª N : ‚Ñï, (1 / 2 ^ N * 2 : ‚Ñù)) at_top (ùìù 0),
    { rw ‚Üê zero_mul (2 : ‚Ñù),
      apply tendsto.mul,
      simp_rw ‚Üê one_div_pow (2 : ‚Ñù),
      apply tendsto_pow_at_top_nhds_0_of_lt_1 ; linarith,
      exact tendsto_const_nhds },
    rcases (at_top_basis.tendsto_iff (nhds_basis_Ioo_pos (0 : ‚Ñù))).mp this Œµ Œµ_pos with ‚ü®N, H, hN‚ü©,
    exact  ‚ü®N, by simpa using (hN N le_rfl).2‚ü© },
  use N,
  intros n hn,
  obtain ‚ü®k, rfl : n = N + k‚ü© := le_iff_exists_add.mp hn,
  calc dist (u (N + k)) (u N) = dist (u (N+0)) (u (N + k)) : by rw [dist_comm, add_zero]
  ... ‚â§ ‚àë i in range k, dist (u (N + i)) (u (N + (i + 1))) : dist_le_range_sum_dist (Œª i, u (N+i)) k
  ... ‚â§ ‚àë i in range k, (1/2 : ‚Ñù) ^ (N+i) : sum_le_sum (Œª i hi, hu $ N+i)
  ... = 1/2^N*‚àë i in range k, (1 / 2)^i : by simp_rw [‚Üê one_div_pow, pow_add, ‚Üê mul_sum]
  ... ‚â§ 1/2^N*2 : mul_le_mul_of_nonneg_left (sum_geometric_two_le _) (one_div_nonneg.mpr (pow_nonneg (zero_le_two : (0 : ‚Ñù) ‚â§ 2)_))
  ... < Œµ : hN
end

/- TEXT:

We are ready for the final boss of this section: Baire's theorem for complete metric spaces!
The proof skeleton below shows interesting techniques. It uses the ``choose`` tactic in its exclamation
mark variant (you should experiment with removing this exclamation mark) and it shows how to
define something inductively in the middle of a proof using ``nat.rec_on``.

TEXT. -/

-- QUOTE:
open metric

example [complete_space X] (f : ‚Ñï ‚Üí set X) (ho : ‚àÄ n, is_open (f n)) (hd : ‚àÄ n, dense (f n)) : dense (‚ãÇn, f n) :=
begin
  let B : ‚Ñï ‚Üí ‚Ñù := Œª n, (1/2)^n,
  have Bpos : ‚àÄ n, 0 < B n, sorry,
  /- Translate the density assumption into two functions `center` and `radius` associating
  to any n, x, Œ¥, Œ¥pos a center and a positive radius such that
  `closed_ball center radius` is included both in `f n` and in `closed_ball x Œ¥`.
  We can also require `radius ‚â§ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : ‚àÄ (n : ‚Ñï)  (x : X) (Œ¥ > 0), ‚àÉ (y : X) (r > 0), r ‚â§ B (n+1) ‚àß closed_ball y r ‚äÜ (closed_ball x Œ¥) ‚à© f n,
  { sorry },
  choose! center radius Hpos HB Hball using this,
  intros x,
  rw mem_closure_iff_nhds_basis nhds_basis_closed_ball,
  intros Œµ Œµpos,
  /- `Œµ` is positive. We have to find a point in the ball of radius `Œµ` around `x` belonging to all
  `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
  `closed_ball (c n) (r n)` is included in the previous ball and in `f n`, and such that
  `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
  limit which belongs to all the `f n`. -/
  let F : ‚Ñï ‚Üí (X √ó ‚Ñù) := Œªn, nat.rec_on n (prod.mk x (min Œµ (B 0)))
                              (Œªn p, prod.mk (center n p.1 p.2) (radius n p.1 p.2)),
  let c : ‚Ñï ‚Üí X := Œª n, (F n).1,
  let r : ‚Ñï ‚Üí ‚Ñù := Œª n, (F n).2,
  have rpos : ‚àÄ n, 0 < r n,
  { sorry },
  
  have rB : ‚àÄn, r n ‚â§ B n,
  { sorry },
  have incl : ‚àÄn, closed_ball (c (n+1)) (r (n+1)) ‚äÜ (closed_ball (c n) (r n)) ‚à© (f n),
  { sorry },
  have cdist : ‚àÄ n, dist (c n) (c (n+1)) ‚â§ B n,
  { sorry },
  have : cauchy_seq c, from cauchy_seq_of_le_geometric_two' cdist,
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchy_seq_tendsto_of_complete this with ‚ü®y, ylim‚ü©,
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Œµ`.
  use y,
  have I : ‚àÄn, ‚àÄ m ‚â• n, closed_ball (c m) (r m) ‚äÜ closed_ball (c n) (r n),
  { sorry },
  have yball : ‚àÄn, y ‚àà closed_ball (c n) (r n),
  { sorry },
  sorry
end

-- QUOTE.

-- SOLUTIONS:
example [complete_space X] (f : ‚Ñï ‚Üí set X) (ho : ‚àÄ n, is_open (f n)) (hd : ‚àÄ n, dense (f n)) : dense (‚ãÇn, f n) :=
begin
  let B : ‚Ñï ‚Üí ‚Ñù := Œª n, (1/2)^n,
  have Bpos : ‚àÄ n, 0 < B n, from Œª n, (pow_pos sorry n),
  /- Translate the density assumption into two functions `center` and `radius` associating
  to any n, x, Œ¥, Œ¥pos a center and a positive radius such that
  `closed_ball center radius` is included both in `f n` and in `closed_ball x Œ¥`.
  We can also require `radius ‚â§ (1/2)^(n+1)`, to ensure we get a Cauchy sequence later. -/
  have : ‚àÄ (n : ‚Ñï)  (x : X) (Œ¥ > 0), ‚àÉ (y : X) (r > 0), r ‚â§ B (n+1) ‚àß closed_ball y r ‚äÜ (closed_ball x Œ¥) ‚à© f n,
  { intros n x Œ¥ Œ¥pos,
    have : x ‚àà closure (f n) := hd n x,
    rcases metric.mem_closure_iff.1 this (Œ¥/2) (half_pos Œ¥pos) with ‚ü®y, ys, xy‚ü©,
    rw dist_comm at xy,
    obtain ‚ü®r, rpos, hr‚ü© : ‚àÉ r > 0, closed_ball y r ‚äÜ f n :=
      nhds_basis_closed_ball.mem_iff.1 (is_open_iff_mem_nhds.1 (ho n) y ys),
    refine ‚ü®y, min (min (Œ¥/2) r) (B (n+1)), _, _, Œªz hz, ‚ü®_, _‚ü©‚ü©,
    show 0 < min (min (Œ¥ / 2) r) (B (n+1)),
      from lt_min (lt_min (half_pos Œ¥pos) rpos) (Bpos (n+1)),
    show min (min (Œ¥ / 2) r) (B (n+1)) ‚â§ B (n+1), from min_le_right _ _,
    show z ‚àà closed_ball x Œ¥, from calc
      dist z x ‚â§ dist z y + dist y x : dist_triangle _ _ _
      ... ‚â§ (min (min (Œ¥ / 2) r) (B (n+1))) + (Œ¥/2) : add_le_add hz xy.le
      ... ‚â§ Œ¥/2 + Œ¥/2 : add_le_add_right ((min_le_left _ _).trans (min_le_left _ _)) _ 
      ... = Œ¥ : add_halves Œ¥,
    show z ‚àà f n, from hr (calc
      dist z y ‚â§ min (min (Œ¥ / 2) r) (B (n+1)) : hz
      ... ‚â§ r : (min_le_left _ _).trans (min_le_right _ _)) },
  choose! center radius Hpos HB Hball using this,
  refine Œª x, (mem_closure_iff_nhds_basis nhds_basis_closed_ball).2 (Œª Œµ Œµpos, _),
  /- `Œµ` is positive. We have to find a point in the ball of radius `Œµ` around `x` belonging to all
  `f n`. For this, we construct inductively a sequence `F n = (c n, r n)` such that the closed ball
  `closed_ball (c n) (r n)` is included in the previous ball and in `f n`, and such that
  `r n` is small enough to ensure that `c n` is a Cauchy sequence. Then `c n` converges to a
  limit which belongs to all the `f n`. -/
  let F : ‚Ñï ‚Üí (X √ó ‚Ñù) := Œªn, nat.rec_on n (prod.mk x (min Œµ (B 0)))
                              (Œªn p, prod.mk (center n p.1 p.2) (radius n p.1 p.2)),
  let c : ‚Ñï ‚Üí X := Œª n, (F n).1,
  let r : ‚Ñï ‚Üí ‚Ñù := Œª n, (F n).2,
  have rpos : ‚àÄ n, 0 < r n,
  { assume n,
    induction n with n hn,
    exact lt_min Œµpos (Bpos 0),
    exact Hpos n (c n) (r n) hn },
  
  have rB : ‚àÄn, r n ‚â§ B n,
  { assume n,
    induction n with n hn,
    exact min_le_right _ _,
    exact HB n (c n) (r n) (rpos n) },
  have incl : ‚àÄn, closed_ball (c (n+1)) (r (n+1)) ‚äÜ (closed_ball (c n) (r n)) ‚à© (f n) :=
    Œª n, Hball n (c n) (r n) (rpos n),
  have cdist : ‚àÄ n, dist (c n) (c (n+1)) ‚â§ B n,
  { assume n,
    rw dist_comm,
    have A : c (n+1) ‚àà closed_ball (c (n+1)) (r (n+1)) := mem_closed_ball_self (rpos $ n +1).le,
    have I := calc
      closed_ball (c (n+1)) (r (n+1)) ‚äÜ closed_ball (c n) (r n) :
         (incl n).trans (inter_subset_left _ _)
      ... ‚äÜ closed_ball (c n) (B n) : closed_ball_subset_closed_ball (rB n),
    exact I A },
  have : cauchy_seq c,
  from cauchy_seq_of_le_geometric_two' cdist,
  -- as the sequence `c n` is Cauchy in a complete space, it converges to a limit `y`.
  rcases cauchy_seq_tendsto_of_complete this with ‚ü®y, ylim‚ü©,
  -- this point `y` will be the desired point. We will check that it belongs to all
  -- `f n` and to `ball x Œµ`.
  use y,
  
  have I : ‚àÄn, ‚àÄ m ‚â• n, closed_ball (c m) (r m) ‚äÜ closed_ball (c n) (r n),
  { assume n,
    refine nat.le_induction _ (Œªm hnm h, _),
    { exact subset.rfl },
    { exact (incl m).trans ((set.inter_subset_left _ _).trans h) }},
  have yball : ‚àÄn, y ‚àà closed_ball (c n) (r n),
  { assume n,
    refine is_closed_ball.mem_of_tendsto ylim _,
    refine (filter.eventually_ge_at_top n).mono (Œª m hm, _),
    exact I n m hm (mem_closed_ball_self (rpos _).le) },
  split,
  { suffices : ‚àÄ n, y ‚àà f n, by rwa set.mem_Inter,    
    intro n,
    have : closed_ball (c (n+1)) (r (n+1)) ‚äÜ f n := subset.trans (incl n) (inter_subset_right _ _),
    exact this (yball (n+1)) },
  calc dist y x ‚â§ r 0 : yball 0
            ... ‚â§ Œµ : min_le_left _ _,
end

/- TEXT:

TEXT. -/

