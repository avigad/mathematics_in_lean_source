import topology.instances.real

open set
open_locale topological_space filter

/- TEXT:
.. _filters:

.. index:: filter

Filters
-------

Filters on a type ``X`` are collections of sets of ``X`` satisfying three conditions. They are mostly used to
abstract two related kinds of ideas:

* *limits*, including all kinds of limits discussed above, in particular finite or infinite limits of sequences, finite or infinite limits of functions at a point or at infinity, etc...

* *things happening eventually*, including things happening for large enough ``n : â„•``, or near enough a point ``x``, or for close enough pairs of points, or things happening almost everywhere in the sense of measure theory. Dually, filters can also express the idea of *things happening often*: for arbitrarily large ``n``, or at a point in any neighborhood of given a point etc...

The examples of filters appearing in these descriptions will be described in detail later, but we can already name them:

* ``(at_top : filter â„•)`` : made of sets of ``â„•`` containing ``{n | n â‰¥ N}`` for some ``N``
* ``ğ“ x`` : made of neighborhoods of ``x`` in a topological space
* ``ğ“¤ X`` : made of entourages of a uniform space (uniform spaces generalize metric spaces and topological groups)
* ``Î¼.a_e`` : made of sets whose complement has zero measure with respect to a measure ``Î¼``.

The general definition is as follows: a filter ``F : filter X`` is a
collection of sets ``F.sets : set (set X)`` such that

* ``F.univ_sets : univ âˆˆ F.sets`` (the set of all inhabitants of ``X`` belongs to ``F.sets``)
* ``F.sets_of_superset : âˆ€ {U V}, U âˆˆ F.sets â†’ U âŠ† V â†’ V âˆˆ F.sets`` (if ``U`` belongs to ``F.sets`` then anything larger also does)
* ``F.inter_sets : âˆ€ {U V}, U âˆˆ sets â†’ V âˆˆ sets â†’ U âˆ© V âˆˆ sets`` (``F.sets`` is stable under intersection)

In mathlib, ``F`` is defined as a structure bundling ``F.sets`` and its
three properties, but the properties carry no additional data, so we
really want to blur the distinction between ``F`` and ``F.sets``. This
is done by defining ``U âˆˆ F`` to mean ``U âˆˆ F.sets``.
However the word sets still appears in some lemma names.

One can see from the axioms above that each filter defines a notion of "sufficiently large" set: the first
condition says that ``univ`` is sufficiently large, the second one says that a set containing a sufficiently 
large set is sufficiently large and the third one says that the intersection of two sufficiently large sets 
is sufficiently large. However a more useful way to see filters is that 
filters on a type ``X`` can be seen as generalized elements of ``set X``. For instance ``at_top`` is the 
"set of very large numbers", and ``ğ“ xâ‚€`` is the "set of points very close to ``xâ‚€``".
A first precise statement is that one can turn any ``s : set X`` into the so-called principal filter
associated to ``s``. This definition is already in mathlib and has a notation ``ğ“Ÿ`` (localized in ``filter``),
but for demonstration purposes we use this as an opportunity to explicitly construct a filter:
TEXT. -/

-- QUOTE:
open filter

def principal {Î± : Type*} (s : set Î±) : filter Î± :=
{ sets := {t | s âŠ† t},
  univ_sets := sorry,
  sets_of_superset := sorry,
  inter_sets := sorry}
-- QUOTE.

-- SOLUTIONS:
-- In the next example we could use `tauto` in each proof instead of knowing the lemmas
example {Î± : Type*} (s : set Î±) : filter Î± :=
{ sets := {t | s âŠ† t},
  univ_sets := subset_univ s, 
  sets_of_superset := Î» U V hU hUV, subset.trans hU hUV,
  inter_sets := Î» U V hU hV, subset_inter hU hV }

/- TEXT:
For our second example, we'll use the filter ``at_top : filter â„•`` (we could use any type with a preorder instead of ``â„•``).
TEXT. -/

-- QUOTE:
example : filter â„• :=
{ sets := {s | âˆƒ a, âˆ€ b, a â‰¤ b â†’ b âˆˆ s},
  univ_sets := sorry,
  sets_of_superset := sorry,
  inter_sets := sorry }
-- QUOTE.

-- SOLUTIONS:
example : filter â„• :=
{ sets := {s | âˆƒ a, âˆ€ b, a â‰¤ b â†’ b âˆˆ s},
  univ_sets := begin
    use 42,
    finish,
  end,
  sets_of_superset := begin
    rintros U V âŸ¨N, hNâŸ© hUV,
    use N,
    tauto,
  end,
  inter_sets := begin
    rintros U V âŸ¨N, hNâŸ© âŸ¨N', hN'âŸ©,
    use max N N',
    intros b hb,
    rw max_le_iff at hb,
    split ; tauto,
  end }

/- TEXT:
We can also directly define the filter ``ğ“ x`` of neighborhoods of any ``x : â„``. 
A neighborhood of ``x`` is a set containing an open interval ``Ioo (x - Îµ) (x + Îµ)``
(of course this is only a special case of a more general construction in mathlib).


With these examples, we can already define what is means for a function ``f : X â†’ Y``
to converge with respect to some ``G : filter Y`` along some ``F : filter X`` as
TEXT. -/
-- QUOTE:
def tendstoâ‚ {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) := 
âˆ€ V âˆˆ G, f â»Â¹' V âˆˆ F
-- QUOTE.

/- TEXT:
When ``X = â„•``, ``Y = â„`` then ``tendstoâ‚ u at_top (ğ“ x)`` is equivalent to asking that the sequence ``u : â„• â†’ â„``
converges to the real number ``x``. When both ``X`` and ``Y`` are ``â„``, ``tendsto f (ğ“ xâ‚€) (ğ“ yâ‚€)`` 
is equivalent to :math:`\lim_{x \to xâ‚€} f(x) = yâ‚€`. All other kinds of limits mentioned in the introduction are
also equivalent to ``tendstoâ‚`` for some filters on source and target.

The definition quoted above is definitionally equivalent to what is in mathlib, but it does not appear in this way,
hence the subscript in the name.
The issue with the above definition is that it exposes a quantifier and elements of ``G``, and it hides the intuition
coming from seeing filters as generalized sets. It is more efficient to
hide the ``âˆ€ V`` and expose the intuition using more algebraic structure. 
The first ingredient is the push-forward operation :math:`f_*` associated to any map ``f : X â†’ Y``:
``filter.map f``. Given a filter ``F`` on ``X``, we get ``filter.map f F : filter Y`` and, definitionnaly,
``V âˆˆ filter.map f F â†” f â»Â¹' V âˆˆ F``. In this file we've opened the filter namespace, so
``filter.map`` can be written as ``map``. This allows rewriting the definition of ``tendsto`` using
the order relation on ``filter Y`` which is reversed inclusion of the set of members:
given ``G H : filter Y``, ``G â‰¤ H â†” âˆ€ V : set Y, V âˆˆ H â†’ V âˆˆ G``.

TEXT. -/

-- QUOTE:
def tendstoâ‚‚ {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) := 
map f F â‰¤ G

example {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) : 
  tendstoâ‚‚ f F G â†” tendstoâ‚ f F G := iff.rfl
-- QUOTE.

/- TEXT:

One could argue that the order relation on filters seems backward. But recall that we can see filters on ``X`` as
generalized elements of ``set X``, through the inclusion of ``ğ“Ÿ : set X â†’ filter X`` given by principal filters. 
This inclusion is order preserving so the order relation on filter can indeed be seen as the "inclusion relation 
between generalized sets". In this analogy, push-forward is analogous to the direct image. 
And indeed ``map f (ğ“Ÿ s) = ğ“Ÿ (f '' s)``. 

We can now understand intuitively why a sequence ``u : â„• â†’ â„`` converges to
a point ``xâ‚€`` if and only if ``map u at_top â‰¤ ğ“ xâ‚€``: this inequality means the "direct image under u" of 
"the set of very big natural numbers" is "included" in "the set of points very close to ``xâ‚€``".

As promised, the above formulation exhibits no quantifier and no set. It also allows leveraging the algebraic property
of the push-forward operation. First each ``filter.map f`` is monotone. And then ``filter.map`` is compatible with
composition.
TEXT. -/

-- QUOTE:
#check @filter.map_mono -- âˆ€ {Î± Î²} {m : Î± â†’ Î²}, monotone (map m)
#check @filter.map_map -- âˆ€ {Î± Î² Î³} {f : filter Î±} {m : Î± â†’ Î²} {m' : Î² â†’ Î³}, map m' (map m f) = map (m' âˆ˜ m) f
-- QUOTE.

/- TEXT:
Together these two properties allow us to prove that limits compose, covering in one shot all 2197 variants from the introduction.
You can practice proving the following statement using either the definition involving the quantifier or the algebraic definition
together with the two above lemmas.
TEXT. -/
-- QUOTE:
example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z} 
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
sorry
-- QUOTE.
-- SOLUTIONS:
example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z} 
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
calc map (g âˆ˜ f) F = map g (map f F) : by rw map_map
... â‰¤ map g G : map_mono hf
... â‰¤ H : hg

example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z} 
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
begin
  intros V hV,
  rw preimage_comp,
  apply hf,
  apply hg,
  exact hV
end

/- TEXT:

The push-forward construction uses a map to push filters from the map source to the map target. 
There also a pull-back operation going in the other direction: ``filter.comap``, generalizing the
preimage operation on sets. For any given map ``f``,
``filter.map f`` and ``filter.comap f`` form a Galois connection: 
``filter.map_le_iff_le_comap : filter.map f F â‰¤ G â†” F â‰¤ filter.comap f G``. 
In particular this operation could be used to provided another formulation of ``tendsto`` that would be propositionally
(but not definitionaly) equivalent to the existing one.

This operation can be used to restrict filters to a subtype. For instance suppose we have ``f : â„ â†’ â„``, 
``xâ‚€ : â„`` and ``yâ‚€ : â„`` and we want to state that ``f x`` goes to ``yâ‚€`` when ``x`` goes to ``xâ‚€`` among rational numbers. 
We can pull-back the filter ``ğ“ xâ‚€`` to ``â„š`` using the coercion map
``coe : â„š â†’ â„`` and state ``tendsto (f âˆ˜ coe : â„š â†’ â„) (comap coe (ğ“ xâ‚€)) (ğ“ yâ‚€)``.
TEXT. -/

-- QUOTE:
variables (f : â„ â†’ â„) (xâ‚€ yâ‚€ : â„)

#check comap (coe : â„š â†’ â„) (ğ“ xâ‚€)
#check tendsto (f âˆ˜ coe) (comap (coe : â„š â†’ â„) (ğ“ xâ‚€)) (ğ“ yâ‚€)
-- QUOTE.

/- TEXT:
The pull-back operation is also compatible with composition, but contravariant.
TEXT. -/

-- QUOTE:
#check @comap_comap
-- QUOTE.

/- TEXT:
Let's now move up to the plane ``â„ Ã— â„`` and try to understand how neighborhoods of some point
``(xâ‚€, yâ‚€)`` are related to ``ğ“ xâ‚€`` and ``ğ“ yâ‚€``. There is a product operation 
``filter.prod : filter X â†’ filter Y â†’ filter (X Ã— Y)``, denoted by ``Ã—á¶ `` which answers this question:
TEXT. -/
-- QUOTE:
example : ğ“ (xâ‚€, yâ‚€) = ğ“ xâ‚€ Ã—á¶  ğ“ yâ‚€ := nhds_prod_eq
-- QUOTE.

/- TEXT:
This operation is defined in term of the pull-back operation and the ``inf`` operation:
``F Ã—á¶  G = (comap prod.fst F) âŠ“ (comap prod.snd G)``.
Here the ``inf`` operation refers to the lattice structure on ``filter X`` for any type ``X``:
``F âŠ“ G`` is the greatest filter that is smaller than both ``F`` and ``G``, generalizing the notion
of the intersection of sets.

A lot of proofs in mathlib use all of the aforementioned structure (``map``, ``comap``, ``inf``, ``sup``, ``prod``) 
to give algebraic proofs about convergence without ever referring to members of filters.
You can practice this on the following lemma, unfolding the definition of ``tendsto``
and ``filter.prod`` if needed.
TEXT. -/

-- QUOTE:
#check le_inf_iff

example (f : â„• â†’ â„ Ã— â„) (xâ‚€ yâ‚€ : â„) : 
  tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†” 
  tendsto (prod.fst âˆ˜ f) at_top (ğ“ xâ‚€) âˆ§ tendsto (prod.snd âˆ˜ f) at_top (ğ“ yâ‚€) :=
sorry
-- QUOTE.

-- SOLUTIONS:

example (f : â„• â†’ â„ Ã— â„) (xâ‚€ yâ‚€ : â„) : 
  tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†” 
  tendsto (prod.fst âˆ˜ f) at_top (ğ“ xâ‚€) âˆ§ tendsto (prod.snd âˆ˜ f) at_top (ğ“ yâ‚€) :=
calc
tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†” map f at_top â‰¤ ğ“ (xâ‚€, yâ‚€) : iff.rfl
... â†” map f at_top â‰¤ ğ“ xâ‚€ Ã—á¶  ğ“ yâ‚€ : by rw nhds_prod_eq
... â†” map f at_top â‰¤ (comap prod.fst (ğ“ xâ‚€) âŠ“ comap prod.snd (ğ“ yâ‚€)) : iff.rfl
... â†” map f at_top â‰¤ comap prod.fst (ğ“ xâ‚€) âˆ§ map f at_top â‰¤ (comap prod.snd (ğ“ yâ‚€)) : le_inf_iff
... â†” map prod.fst (map f at_top) â‰¤ ğ“ xâ‚€ âˆ§ map prod.snd (map f at_top) â‰¤ ğ“ yâ‚€ : 
        by rw [â† map_le_iff_le_comap, â† map_le_iff_le_comap]
... â†” map (prod.fst âˆ˜ f) at_top â‰¤ ğ“ xâ‚€ âˆ§ map (prod.snd âˆ˜ f) at_top â‰¤ ğ“ yâ‚€ : by rw [map_map, map_map]
/- begin
  rw nhds_prod_eq,
  unfold tendsto filter.prod,
  rw [le_inf_iff, â† map_le_iff_le_comap, map_map, â† map_le_iff_le_comap, map_map]
end -/

/- TEXT:
The ordered type ``filter X`` is actually a *complete* lattice: there is a bottom element and a top element and
any set of filters on ``X`` have an ``Inf`` and a ``Sup``.

Note that in the definition of filters, given the second property
(if ``U`` belongs to ``F`` then anything larger also belongs), the first property 
(the set of all inhabitants of ``X`` belongs to ``F``) is
equivalent to asking that ``F`` is not the empty collection of sets.
This shouldn't be confused with a more subtle question. The 
definition does not prohibit ``âˆ… âˆˆ F``. If ``âˆ… âˆˆ F`` then
``âˆ€ U : set X, U âˆˆ F``, hence ``F`` is a
rather trivial filter. This trivial filter is precisely the bottom element of the complete
lattice ``filter X``.
This contrasts with the definition of filters in
Bourbaki who doesn't allow filters containing the empty set. 
Because of this choice, we need an
extra assumption of non-triviality in some lemmas, but gain
nicer global formal properties for the theory. We've seen already that it gives us a
bottom element. It also allowed us to define ``principal : set X â†’ filter X`` without
precondition (this map sends ``âˆ…`` to ``âŠ¥``). 
And it allowed to get the pull-back operation without precondition.
Indeed it can happen that ``comap f F = âŠ¥`` although ``F â‰  âŠ¥``. For instance,
given ``xâ‚€ : â„`` and ``s : set â„``, the pull-back of ``ğ“ xâ‚€`` under the coercion
from the subtype corresponding to ``s`` is non-trivial if and only if ``xâ‚€`` belongs to the
closure of ``s``.

In order to easily manage lemmas that do need to assume some filter is non-trivial, there is
a type class ``filter.ne_bot``, so you sometimes see lemmas assuming 
``(F : filter X) [F.ne_bot]``. The instance database knows for instance that ``(at_top : filter â„•).ne_bot``
and knows that pushing forward a non-trivial filter gives a non-trivial filter, so a lemma
assuming ``[F.ne_bot]`` will automatically apply to ``map u at_top`` for any sequence ``u``.

Our tour of algebraic properties of filters and their relation to limits is essentially done,
but careful readers may have noticed that our claim that we get back the usual notions
of limits is not so clear. Superficially, it may look like ``tendsto u at_top (ğ“ xâ‚€)``
is stronger than the usual condition because we ask that *every* neighborhood of ``xâ‚€`` 
has a preimage belonging to ``at_top`` whereas the usual definition only asks
this for the standard neighborhoods ``Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ)``. 
The key is that, by definition, every neighborhood contains such a standard one.
This idea leads to the notion of a filter basis. Given ``F : filter X``,
a family of sets `s : Î¹ â†’ set X` is a basis for ``F`` if 
``âˆ€ U : set X, U âˆˆ F â†” âˆƒ i, s i âŠ† U``. Actually it is more flexible to also consider
a predicate on ``Î¹`` to select only some values ``i``. In the case of
``ğ“ xâ‚€``, we want ``Î¹`` to be ``â„``, ``i = Îµ`` and the predicate should select
positive ``Îµ``. So the actual basis statement is:
TEXT. -/

-- QUOTE:
example (xâ‚€ : â„) : has_basis (ğ“ xâ‚€) (Î» Îµ : â„, 0 < Îµ) (Î» Îµ, Ioo  (xâ‚€ - Îµ) (xâ‚€ + Îµ)) :=
nhds_basis_Ioo_pos xâ‚€
-- QUOTE.

/- TEXT:
Of course we also have a nice basis for ``at_top``, and the lemma ``filter.has_basis.tendsto_iff`` allows
us to reformulate a ``tendsto f F G`` given bases for ``F`` and ``G``.
TEXT. -/

-- QUOTE:
example (u : â„• â†’ â„) (xâ‚€ : â„) : 
  tendsto u at_top (ğ“ xâ‚€) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, u n âˆˆ Ioo  (xâ‚€ - Îµ) (xâ‚€ + Îµ) :=
begin
  have : at_top.has_basis (Î» n : â„•, true) Ici := at_top_basis,
  rw this.tendsto_iff (nhds_basis_Ioo_pos xâ‚€),
  simp
end
-- QUOTE.

/- TEXT:
We now turn to the way filters allow to efficiently handle properties that hold for sufficiently large numbers
or sufficiently close to a given point. If you did the exercises in :numref:`sequences_and_convergence`,
you have very often been in the situation where some property ``P n`` holds for sufficiently large ``n`` and some
other property ``Q n`` holds for sufficiently large ``n``. After two ```cases`` this gave you
``N_P`` and ``N_Q`` such that ``âˆ€ n â‰¥ N_P, P n`` and ``âˆ€ n â‰¥ N_Q, Q n``. Then you ``set N := max N_P N_Q`` and
patiently proved that ``âˆ€ n â‰¥ N, P n âˆ§ Q n``. After doing this a couple of time this feels very tiresome. 

The way to solve this issue is to note that "``P n`` and ``Q n`` hold for large enough ``n``" means 
``{n | P n} âˆˆ at_top`` and ``{n | Q n} âˆˆ at_top``.
Then the fact that ``at_top`` is a filter means in particular that the intersection of two elements of ``at_top`` 
is again in ``at_top``, so ``{n | P n âˆ§ Q n} âˆˆ at_top``. However writing ``{n | P n} âˆˆ at_top`` is not very nice.
In this situation, we use the more suggestive notation ``âˆ€á¶  n in at_top, P n``. 
Note the superscript ``f`` which stands for filter. 
The notation reads: for all ``n`` in "the set of very large numbers", ``P n`` holds. Technically, this notation 
stands for ``filter.eventually``, and the lemma ``filter.eventually.and`` uses the intersection property of filters to do
what we described above:

TEXT. -/

-- QUOTE:
example (P Q : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n) : 
  âˆ€á¶  n in at_top, P n âˆ§ Q n := hP.and hQ
-- QUOTE.

/- TEXT:
This notation is so convenient and intuitive to use that we also have specializations 
when ``P`` is an equality or inequality statement. For instance, let ``u`` and ``v`` be
two sequences of real numbers. Let us state that if
``u n`` and ``v n`` coincide for large enough ``n`` then ``u`` tends to ``xâ‚€`` if and only
if ``v`` tends to ``xâ‚€``. First we'll use the generic ``eventually`` and then the one 
specialized for the equality predicate, ``eventually_eq`` . Note that both statements are
definitionaly equivalent so the same proof will be used.

TEXT. -/

-- QUOTE:
example (u v : â„• â†’ â„) (h : âˆ€á¶  n in at_top, u n = v n) (xâ‚€ : â„) : 
  tendsto u at_top (ğ“ xâ‚€) â†” tendsto v at_top (ğ“ xâ‚€) :=
tendsto_congr' h

example (u v : â„• â†’ â„) (h : u =á¶ [at_top] v) (xâ‚€ : â„) : 
  tendsto u at_top (ğ“ xâ‚€) â†” tendsto v at_top (ğ“ xâ‚€) :=
tendsto_congr' h

-- QUOTE.

/- TEXT:

It instructive to review the definition of filters in terms of this use with ``eventually``.
Given ``F : filter X``, for any predicates ``P`` and ``Q`` on ``X``,

* the condition ``univ âˆˆ F`` ensures that ``(âˆ€ x, P x) â†’ âˆ€á¶  x in F, P x``.
* the condition ``U âˆˆ F â†’ U âŠ† V â†’ V âˆˆ F`` ensures that ``(âˆ€á¶  x in F, P x) â†’ (âˆ€ x, P x â†’ Q x) â†’ âˆ€á¶  x in F, Q x``
* the condition ``U âˆˆ F â†’ V âˆˆ F â†’ U âˆ© V âˆˆ F`` ensures that ``(âˆ€á¶  x in F, P x) â†’ (âˆ€á¶  x in F, Q x) â†’ âˆ€á¶  x in F, P x âˆ§ Q x``, as we already discussed.

TEXT. -/

-- QUOTE:
#check @eventually_of_forall
#check @eventually.mono
#check @eventually.and
-- QUOTE.

/- TEXT:
The item above, corresponding to ``eventually.mono`` is also a very nice use of filters, especially combined
with ``eventually.and``. The ``filter_upwards`` tactic allows us to nicely combine them. 
Compare:

TEXT. -/

-- QUOTE:

example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n) 
  (hR : âˆ€á¶  n in at_top, P n âˆ§ Q n â†’ R n) : 
  âˆ€á¶  n in at_top, R n := 
begin
  apply (hP.and (hQ.and hR)).mono,
  rintros n âŸ¨h, h', h''âŸ©,
  exact h'' âŸ¨h, h'âŸ©
end

example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n) 
  (hR : âˆ€á¶  n in at_top, P n âˆ§ Q n â†’ R n) : 
  âˆ€á¶  n in at_top, R n := 
begin
  filter_upwards [hP, hQ, hR],
  intros n h h' h'',
  exact h'' âŸ¨h, h'âŸ©
end

-- QUOTE.

/- TEXT:

Readers who know about measure theory will note that the filter ``Î¼.ae`` of sets whose complement has measure zero
(aka "the set of almost every point") is not very useful as the source or target of ``tendsto`` but very often
used with ``eventually``, when saying a property holds for almost every point.

There is a dual version of ``âˆ€á¶  x in F, P x`` which is much less useful, but still exists: ``âˆƒá¶  x in F, P x`` means
``{x | Â¬P x} âˆ‰ f``. For instance ``âˆƒá¶  n in at_top, P n`` means there are arbitrarily large ``n`` such that ``P n``.
This notation stands for ``filter.frequently``.


As a more sophisticated example, the statement: "if a sequence ``u`` converges to
some ``x`` and ``u n`` belongs to a set ``M`` for ``n`` large enough then ``x`` is in the closure of
``M``" is formalized as: ``tendsto u at_top (ğ“ x) â†’ (âˆ€á¶  n in at_top, u n âˆˆ M) â†’ x âˆˆ closure M``,
which is a special case of ``mem_closure_of_tendsto`` from the topology library.
See if you can prove it using the quoted lemmas, knowing that ``cluster_pt x F`` means ``(ğ“ x âŠ“ F).ne_bot``.

TEXT. -/
-- QUOTE:
#check mem_closure_iff_cluster_pt
#check le_principal_iff
#check ne_bot_of_le

example (u : â„• â†’ â„) (M : set â„) (x : â„) 
  (hux : tendsto u at_top (ğ“ x)) (huM : âˆ€á¶  n in at_top, u n âˆˆ M) : x âˆˆ closure M :=
sorry
-- QUOTE.

-- SOLUTIONS:
example (u : â„• â†’ â„) (M : set â„) (x : â„) 
  (hux : tendsto u at_top (ğ“ x)) (huM : âˆ€á¶  n in at_top, u n âˆˆ M) : x âˆˆ closure M :=
mem_closure_iff_cluster_pt.mpr (ne_bot_of_le $ le_inf hux $ le_principal_iff.mpr huM)
