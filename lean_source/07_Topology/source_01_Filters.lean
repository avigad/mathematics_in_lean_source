import topology.instances.real

open set filter
open_locale topological_space filter

/- TEXT:
.. index:: filter

.. _filters:

Filters
-------

A *filter* on a type ``X`` is a collection of sets of ``X`` that satisfies three
conditions that we will spell out below. The notion
supports two related ideas:

* *limits*, including all the kinds of limits discussed above: finite and infinite limits of sequences, finite and infinite limits of functions at a point or at infinity, and so on.

* *things happening eventually*, including things happening for large enough ``n : â„•``, or sufficiently near a point ``x``, or for sufficiently close pairs of points, or almost everywhere in the sense of measure theory. Dually, filters can also express the idea of *things happening often*: for arbitrarily large ``n``, at a point in any neighborhood of given a point, etc.

The filters that correspond to these descriptions will be defined later in this section, but we can already name them:

* ``(at_top : filter â„•)``, made of sets of ``â„•`` containing ``{n | n â‰¥ N}`` for some ``N``
* ``ğ“ x``, made of neighborhoods of ``x`` in a topological space
* ``ğ“¤ X``, made of entourages of a uniform space (uniform spaces generalize metric spaces and topological groups)
* ``Î¼.a_e`` , made of sets whose complement has zero measure with respect to a measure ``Î¼``.

The general definition is as follows: a filter ``F : filter X`` is a
collection of sets ``F.sets : set (set X)`` satisfying the following:

* ``F.univ_sets : univ âˆˆ F.sets``
* ``F.sets_of_superset : âˆ€ {U V}, U âˆˆ F.sets â†’ U âŠ† V â†’ V âˆˆ F.sets``
* ``F.inter_sets : âˆ€ {U V}, U âˆˆ F.sets â†’ V âˆˆ F.sets â†’ U âˆ© V âˆˆ F.sets``.

The first condition says that the set of all elements of ``X`` belongs to ``F.sets``.
The second condition says that if ``U`` belongs to ``F.sets`` then anything
containing ``U`` also belongs to ``F.sets``.
The third condition says that ``F.sets`` is closed under finite intersections.
In mathlib, a filter ``F`` is defined to be a structure bundling ``F.sets`` and its
three properties, but the properties carry no additional data,
and it is convenient to blur the distinction between ``F`` and ``F.sets``. We
therefore define ``U âˆˆ F`` to mean ``U âˆˆ F.sets``.
This explains why the word ``sets`` appears in the names of some lemmas that
that mention ``U âˆˆ F``.

It may help to think of a filter as defining a notion of a "sufficiently large" set. The first
condition then says that ``univ`` is sufficiently large, the second one says that a set containing a sufficiently
large set is sufficiently large and the third one says that the intersection of two sufficiently large sets
is sufficiently large.

It may be even  more useful to think of a filter on a type ``X``
as a generalized element of ``set X``. For instance, ``at_top`` is the
"set of very large numbers" and ``ğ“ xâ‚€`` is the "set of points very close to ``xâ‚€``."
One manifestation of this view is that we can associate to any ``s : set X`` the so-called *principal filter*
consisting of all sets that contain ``s``.
This definition is already in mathlib and has a notation ``ğ“Ÿ`` (localized in the ``filter`` namespace).
For the purpose of demonstration, we ask you to take this opportunity to work out the definition here.
EXAMPLES: -/
-- QUOTE:
def principal {Î± : Type*} (s : set Î±) : filter Î± :=
{ sets := {t | s âŠ† t},
  univ_sets := sorry,
  sets_of_superset := sorry,
  inter_sets := sorry}
-- QUOTE.

-- SOLUTIONS:
-- In the next example we could use `tauto` in each proof instead of knowing the lemmas
example {Î± : Type*} (s : set Î±) : filter Î± :=
{ sets := {t | s âŠ† t},
  univ_sets := subset_univ s,
  sets_of_superset := Î» U V hU hUV, subset.trans hU hUV,
  inter_sets := Î» U V hU hV, subset_inter hU hV }

/- TEXT:
For our second example, we ask you to define the filter ``at_top : filter â„•``.
(We could use any type with a preorder instead of ``â„•``.)
EXAMPLES: -/
-- QUOTE:
example : filter â„• :=
{ sets := {s | âˆƒ a, âˆ€ b, a â‰¤ b â†’ b âˆˆ s},
  univ_sets := sorry,
  sets_of_superset := sorry,
  inter_sets := sorry }
-- QUOTE.

-- SOLUTIONS:
example : filter â„• :=
{ sets := {s | âˆƒ a, âˆ€ b, a â‰¤ b â†’ b âˆˆ s},
  univ_sets := begin
    use 42,
    finish,
  end,
  sets_of_superset := begin
    rintros U V âŸ¨N, hNâŸ© hUV,
    use N,
    tauto,
  end,
  inter_sets := begin
    rintros U V âŸ¨N, hNâŸ© âŸ¨N', hN'âŸ©,
    use max N N',
    intros b hb,
    rw max_le_iff at hb,
    split ; tauto,
  end }

/- TEXT:
We can also directly define the filter ``ğ“ x`` of neighborhoods of any ``x : â„``.
In the real numbers, a neighborhood of ``x`` is a set containing an open interval
:math:`(x_0 - \varepsilon, x_0 + \varepsilon)`,
defined in mathlib as ``Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ)``.
(This is notion of a neighborhood is only a special case of a more general construction in mathlib.)

With these examples, we can already define what is means for a function ``f : X â†’ Y``
to converge to some ``G : filter Y`` along some ``F : filter X``,
as follows:
BOTH: -/
-- QUOTE:
def tendstoâ‚ {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) :=
âˆ€ V âˆˆ G, f â»Â¹' V âˆˆ F
-- QUOTE.

/- TEXT:
When ``X`` is ``â„•`` and ``Y`` is ``â„``, ``tendstoâ‚ u at_top (ğ“ x)`` is equivalent to saying that the sequence ``u : â„• â†’ â„``
converges to the real number ``x``. When both ``X`` and ``Y`` are ``â„``, ``tendsto f (ğ“ xâ‚€) (ğ“ yâ‚€)``
is equivalent to the familiar notion :math:`\lim_{x \to xâ‚€} f(x) = yâ‚€`.
All of the other kinds of limits mentioned in the introduction are
also equivalent to instances of ``tendstoâ‚`` for suitable choices of filters on the source and target.

The notion ``tendstoâ‚`` above is definitionally equivalent to the notion ``tendsto`` that is defined in mathlib,
but the latter is defined more abstractly.
The problem with the definition of ``tendstoâ‚`` is that it exposes a quantifier and elements of ``G``,
and it hides the intuition that we get by viewing filters as generalized sets. We can
hide the quantifier ``âˆ€ V`` and make the intuition more salient by using more algebraic and set-theoretic machinery.
The first ingredient is the *pushforward* operation :math:`f_*` associated to any map ``f : X â†’ Y``,
denoted ``filter.map f`` in mathlib. Given a filter ``F`` on ``X``, ``filter.map f F : filter Y`` is defined so that
``V âˆˆ filter.map f F â†” f â»Â¹' V âˆˆ F`` holds definitionally.
In this examples file we've opened the ``filter`` namespace so that
``filter.map`` can be written as ``map``. This means that we can rewrite the definition of ``tendsto`` using
the order relation on ``filter Y``, which is reversed inclusion of the set of members.
In other words, given ``G H : filter Y``, we have ``G â‰¤ H â†” âˆ€ V : set Y, V âˆˆ H â†’ V âˆˆ G``.
EXAMPLES: -/
-- QUOTE:
def tendstoâ‚‚ {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) :=
map f F â‰¤ G

example {X Y : Type*} (f : X â†’ Y) (F : filter X) (G : filter Y) :
  tendstoâ‚‚ f F G â†” tendstoâ‚ f F G := iff.rfl
-- QUOTE.

/- TEXT:
It may seem that the order relation on filters is backward. But recall that we can view filters on ``X`` as
generalized elements of ``set X``, via the inclusion of ``ğ“Ÿ : set X â†’ filter X`` which maps any set ``s`` to the corresponding principal filter.
This inclusion is order preserving, so the order relation on ``filter`` can indeed be seen as the natural inclusion relation
between generalized sets. In this analogy, pushforward is analogous to the direct image.
And, indeed, ``map f (ğ“Ÿ s) = ğ“Ÿ (f '' s)``.

We can now understand intuitively why a sequence ``u : â„• â†’ â„`` converges to
a point ``xâ‚€`` if and only if we have ``map u at_top â‰¤ ğ“ xâ‚€``.
The inequality means the "direct image under ``u``" of
"the set of very big natural numbers" is "included" in "the set of points very close to ``xâ‚€``."

As promised, the definition of ``tendstoâ‚‚`` does not exhibit any quantifiers or sets.
It also leverages the algebraic properties of the pushforward operation.
First, each ``filter.map f`` is monotone. And, second, ``filter.map`` is compatible with
composition.
EXAMPLES: -/
-- QUOTE:
#check (@filter.map_mono : âˆ€ {Î± Î²} {m : Î± â†’ Î²}, monotone (map m))
#check (@filter.map_map : âˆ€ {Î± Î² Î³} {f : filter Î±} {m : Î± â†’ Î²} {m' : Î² â†’ Î³},
                            map m' (map m f) = map (m' âˆ˜ m) f)
-- QUOTE.

/- TEXT:
Together these two properties allow us to prove that limits compose, yielding in one shot all 256 variants
of the composition lemma described in the introduction, and lots more.
You can practice proving the following statement using either the definition
of ``tendstoâ‚`` in terms of the
universal quantifier or the algebraic definition,
together with the two lemmas above.
EXAMPLES: -/
-- QUOTE:
example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z}
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
sorry
-- QUOTE.

-- SOLUTIONS:
example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z}
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
calc map (g âˆ˜ f) F = map g (map f F) : by rw map_map
... â‰¤ map g G : map_mono hf
... â‰¤ H : hg

example {X Y Z : Type*} {F : filter X} {G : filter Y} {H : filter Z} {f : X â†’ Y} {g : Y â†’ Z}
  (hf : tendstoâ‚ f F G) (hg : tendstoâ‚ g G H) : tendstoâ‚ (g âˆ˜ f) F H :=
begin
  intros V hV,
  rw preimage_comp,
  apply hf,
  apply hg,
  exact hV
end

/- TEXT:
The pushforward construction uses a map to push filters from the map source to the map target.
There also a *pullback* operation, ``filter.comap``, going in the other direction.
This generalizes the
preimage operation on sets. For any map ``f``,
``filter.map f`` and ``filter.comap f`` form what is known as a *Galois connection*,
which is to say, they satisfy

  ``filter.map_le_iff_le_comap : filter.map f F â‰¤ G â†” F â‰¤ filter.comap f G``

for every ``F`` and ``G``.
This operation could be used to provided another formulation of ``tendsto`` that would be provably
(but not definitionaly) equivalent to the one in mathlib.

The ``comap`` operation can be used to restrict filters to a subtype. For instance, suppose we have ``f : â„ â†’ â„``,
``xâ‚€ : â„`` and ``yâ‚€ : â„``, and suppose we want to state that ``f x`` approaches ``yâ‚€`` when ``x`` approaches ``xâ‚€`` within the rational numbers.
We can pull the filter ``ğ“ xâ‚€`` back to ``â„š`` using the coercion map
``coe : â„š â†’ â„`` and state ``tendsto (f âˆ˜ coe : â„š â†’ â„) (comap coe (ğ“ xâ‚€)) (ğ“ yâ‚€)``.
EXAMPLES: -/
-- QUOTE:
variables (f : â„ â†’ â„) (xâ‚€ yâ‚€ : â„)

#check comap (coe : â„š â†’ â„) (ğ“ xâ‚€)
#check tendsto (f âˆ˜ coe) (comap (coe : â„š â†’ â„) (ğ“ xâ‚€)) (ğ“ yâ‚€)
-- QUOTE.

/- TEXT:
The pullback operation is also compatible with composition, but it *contravariant*,
which is to say, it reverses the order of the arguments.
EXAMPLES: -/
-- QUOTE:
section
variables {Î± Î² Î³ : Type*} (F : filter Î±) {m : Î³ â†’ Î²} {n : Î² â†’ Î±}

#check (comap_comap : comap m (comap n F) = comap (n âˆ˜ m) F)
end
-- QUOTE.

/- TEXT:
Let's now shift attention to the plane ``â„ Ã— â„`` and try to understand how the neighborhoods of a point
``(xâ‚€, yâ‚€)`` are related to ``ğ“ xâ‚€`` and ``ğ“ yâ‚€``. There is a product operation
``filter.prod : filter X â†’ filter Y â†’ filter (X Ã— Y)``, denoted by ``Ã—á¶ ``, which answers this question:
EXAMPLES: -/
-- QUOTE:
example : ğ“ (xâ‚€, yâ‚€) = ğ“ xâ‚€ Ã—á¶  ğ“ yâ‚€ := nhds_prod_eq
-- QUOTE.

/- TEXT:
The product operation is defined in terms of the pullback operation and the ``inf`` operation:

  ``F Ã—á¶  G = (comap prod.fst F) âŠ“ (comap prod.snd G)``.

Here the ``inf`` operation refers to the lattice structure on ``filter X`` for any type ``X``, whereby
``F âŠ“ G`` is the greatest filter that is smaller than both ``F`` and ``G``.
Thus the ``inf`` operation generalizes the notion of the intersection of sets.

A lot of proofs in mathlib use all of the aforementioned structure (``map``, ``comap``, ``inf``, ``sup``, and ``prod``)
to give algebraic proofs about convergence without ever referring to members of filters.
You can practice doing this in a proof of the following lemma, unfolding the definition of ``tendsto``
and ``filter.prod`` if needed.
EXAMPLES: -/
-- QUOTE:
#check le_inf_iff

example (f : â„• â†’ â„ Ã— â„) (xâ‚€ yâ‚€ : â„) :
  tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†”
    tendsto (prod.fst âˆ˜ f) at_top (ğ“ xâ‚€) âˆ§ tendsto (prod.snd âˆ˜ f) at_top (ğ“ yâ‚€) :=
sorry
-- QUOTE.

-- SOLUTIONS:
example (f : â„• â†’ â„ Ã— â„) (xâ‚€ yâ‚€ : â„) :
  tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†”
  tendsto (prod.fst âˆ˜ f) at_top (ğ“ xâ‚€) âˆ§ tendsto (prod.snd âˆ˜ f) at_top (ğ“ yâ‚€) :=
calc
tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†” map f at_top â‰¤ ğ“ (xâ‚€, yâ‚€) : iff.rfl
... â†” map f at_top â‰¤ ğ“ xâ‚€ Ã—á¶  ğ“ yâ‚€ : by rw nhds_prod_eq
... â†” map f at_top â‰¤ (comap prod.fst (ğ“ xâ‚€) âŠ“ comap prod.snd (ğ“ yâ‚€)) : iff.rfl
... â†” map f at_top â‰¤ comap prod.fst (ğ“ xâ‚€) âˆ§ map f at_top â‰¤ (comap prod.snd (ğ“ yâ‚€)) : le_inf_iff
... â†” map prod.fst (map f at_top) â‰¤ ğ“ xâ‚€ âˆ§ map prod.snd (map f at_top) â‰¤ ğ“ yâ‚€ :
        by rw [â† map_le_iff_le_comap, â† map_le_iff_le_comap]
... â†” map (prod.fst âˆ˜ f) at_top â‰¤ ğ“ xâ‚€ âˆ§ map (prod.snd âˆ˜ f) at_top â‰¤ ğ“ yâ‚€ : by rw [map_map, map_map]

-- an alterantive solution
example (f : â„• â†’ â„ Ã— â„) (xâ‚€ yâ‚€ : â„) :
  tendsto f at_top (ğ“ (xâ‚€, yâ‚€)) â†”
  tendsto (prod.fst âˆ˜ f) at_top (ğ“ xâ‚€) âˆ§ tendsto (prod.snd âˆ˜ f) at_top (ğ“ yâ‚€) :=
begin
  rw nhds_prod_eq,
  unfold tendsto filter.prod,
  rw [le_inf_iff, â† map_le_iff_le_comap, map_map, â† map_le_iff_le_comap, map_map]
end

/- TEXT:
The ordered type ``filter X`` is actually a *complete* lattice,
which is to say, there is a bottom element, there is a top element, and
every set of filters on ``X`` has an ``Inf`` and a ``Sup``.

Note that given the second property in the definition of a filter
(if ``U`` belongs to ``F`` then anything larger than ``U`` also belongs to ``F``),
the first property
(the set of all inhabitants of ``X`` belongs to ``F``) is
equivalent to the property that ``F`` is not the empty collection of sets.
This shouldn't be confused with the more subtle question as to whether
the empty set is an *element* of ``F``. The
definition of a filter does not prohibit ``âˆ… âˆˆ F``,
but if the empty set is in ``F`` then
every set is in ``F``, which is to say, ``âˆ€ U : set X, U âˆˆ F``.
In this case, ``F`` is a rather trivial filter, which is precisely the
bottom element of the complete lattice ``filter X``.
This contrasts with the definition of filters in
Bourbaki, which doesn't allow filters containing the empty set.

Because we include the trivial filter in our definition, we sometimes need to explicitly assume
nontriviality in some lemmas.
In return, however, the theory has nicer global properties.
We have already seen that including the trivial filter gives us a
bottom element. It also allows us to define ``principal : set X â†’ filter X``,
which maps  ``âˆ…`` to ``âŠ¥``, without additing a precondition to rule out the empty set.
And it allows us to define the pullback operation without a precondition as well.
Indeed, it can happen that ``comap f F = âŠ¥`` although ``F â‰  âŠ¥``. For instance,
given ``xâ‚€ : â„`` and ``s : set â„``, the pullback of ``ğ“ xâ‚€`` under the coercion
from the subtype corresponding to ``s`` is nontrivial if and only if ``xâ‚€`` belongs to the
closure of ``s``.

In order to manage lemmas that do need to assume some filter is nontrivial, mathlib has
a type class ``filter.ne_bot``, and the library has lemmas that assume
``(F : filter X) [F.ne_bot]``. The instance database knows, for example, that ``(at_top : filter â„•).ne_bot``,
and it knows that pushing forward a nontrivial filter gives a nontrivial filter.
As a result, a lemma assuming ``[F.ne_bot]`` will automatically apply to ``map u at_top`` for any sequence ``u``.

Our tour of the algebraic properties of filters and their relation to limits is essentially done,
but we have not yet justified our claim to have recaptured the usual limit notions.
Superficially, it may seem that ``tendsto u at_top (ğ“ xâ‚€)``
is stronger than the notion of convergence defined in :numref:`sequences_and_convergence` because we ask that *every* neighborhood of ``xâ‚€``
has a preimage belonging to ``at_top``, whereas the usual definition only requires
this for the standard neighborhoods ``Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ)``.
The key is that, by definition, every neighborhood contains such a standard one.
This observation leads to the notion of a *filter basis*.

Given ``F : filter X``,
a family of sets `s : Î¹ â†’ set X` is a basis for ``F`` if for every set ``U``,
we have ``U âˆˆ F`` if and only if it contains some ``s i``. In other words, formally speaking,
``s`` is a basis if it satisfies
``âˆ€ U : set X, U âˆˆ F â†” âˆƒ i, s i âŠ† U``. It is even more flexible to consider
a predicate on ``Î¹`` that selects only some of the values ``i`` in the indexing type.
In the case of ``ğ“ xâ‚€``, we want ``Î¹`` to be ``â„``, we write ``Îµ`` for ``i``, and the predicate should select the positive values of ``Îµ``.
So the fact that the sets ``Ioo  (xâ‚€ - Îµ) (xâ‚€ + Îµ)`` form a basis for the
neighborhood topology on ``â„`` is stated as follows:
EXAMPLES: -/
-- QUOTE:
example (xâ‚€ : â„) : has_basis (ğ“ xâ‚€) (Î» Îµ : â„, 0 < Îµ) (Î» Îµ, Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ)) :=
nhds_basis_Ioo_pos xâ‚€
-- QUOTE.

/- TEXT:
There is also a nice basis for the filter ``at_top``. The lemma
``filter.has_basis.tendsto_iff`` allows
us to reformulate a statement of the form ``tendsto f F G``
given bases for ``F`` and ``G``.
Putting these pieces together gives us essentially the notion of convergence
that we used in :numref:`sequences_and_convergence`.
EXAMPLES: -/
-- QUOTE:
example (u : â„• â†’ â„) (xâ‚€ : â„) :
  tendsto u at_top (ğ“ xâ‚€) â†” âˆ€ Îµ > 0, âˆƒ N, âˆ€ n â‰¥ N, u n âˆˆ Ioo (xâ‚€ - Îµ) (xâ‚€ + Îµ) :=
begin
  have : at_top.has_basis (Î» n : â„•, true) Ici := at_top_basis,
  rw this.tendsto_iff (nhds_basis_Ioo_pos xâ‚€),
  simp
end
-- QUOTE.

/- TEXT:
We now show how filters facilitate working with properties that hold for sufficiently large numbers
or for points that are sufficiently close to a given point. In :numref:`sequences_and_convergence`, we were often faced with the situation where
we knew that some property ``P n`` holds for sufficiently large ``n`` and that some
other property ``Q n`` holds for sufficiently large ``n``.
Using ``cases`` twice gave us ``N_P`` and ``N_Q`` satisfying
``âˆ€ n â‰¥ N_P, P n`` and ``âˆ€ n â‰¥ N_Q, Q n``. Using ``set N := max N_P N_Q``, we could
eventually prove ``âˆ€ n â‰¥ N, P n âˆ§ Q n``.
Doing this repeatedly becomes tiresome.

We can do better by noting that the statement "``P n`` and ``Q n`` hold for large enough ``n``" means
that we have ``{n | P n} âˆˆ at_top`` and ``{n | Q n} âˆˆ at_top``.
The fact that ``at_top`` is a filter implies that the intersection of two elements of ``at_top``
is again in ``at_top``, so we have ``{n | P n âˆ§ Q n} âˆˆ at_top``.
Writing ``{n | P n} âˆˆ at_top`` is unpleasant,
but we can use the more suggestive notation ``âˆ€á¶  n in at_top, P n``.
Here the superscripted ``f`` stands for "filter."
You can think of the notation as saying that for all ``n`` in the "set of very large numbers," ``P n`` holds. The ``âˆ€á¶ ``
notation stands for ``filter.eventually``, and the lemma ``filter.eventually.and`` uses the intersection property of filters to do what we just described:
EXAMPLES: -/
-- QUOTE:
example (P Q : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n) :
  âˆ€á¶  n in at_top, P n âˆ§ Q n := hP.and hQ
-- QUOTE.

/- TEXT:
This notation is so convenient and intuitive that we also have specializations
when ``P`` is an equality or inequality statement. For example, let ``u`` and ``v`` be
two sequences of real numbers, and let us show that if
``u n`` and ``v n`` coincide for sufficiently large ``n`` then
``u`` tends to ``xâ‚€`` if and only if ``v`` tends to ``xâ‚€``.
First we'll use the generic ``eventually`` and then the one
specialized for the equality predicate, ``eventually_eq``. The two statements are
definitionaly equivalent so the same proof work in both cases.
EXAMPLES: -/
-- QUOTE:
example (u v : â„• â†’ â„) (h : âˆ€á¶  n in at_top, u n = v n) (xâ‚€ : â„) :
  tendsto u at_top (ğ“ xâ‚€) â†” tendsto v at_top (ğ“ xâ‚€) :=
tendsto_congr' h

example (u v : â„• â†’ â„) (h : u =á¶ [at_top] v) (xâ‚€ : â„) :
  tendsto u at_top (ğ“ xâ‚€) â†” tendsto v at_top (ğ“ xâ‚€) :=
tendsto_congr' h
-- QUOTE.

/- TEXT:
It is instructive to review the definition of filters in terms of ``eventually``.
Given ``F : filter X``, for any predicates ``P`` and ``Q`` on ``X``,

* the condition ``univ âˆˆ F`` ensures ``(âˆ€ x, P x) â†’ âˆ€á¶  x in F, P x``,
* the condition ``U âˆˆ F â†’ U âŠ† V â†’ V âˆˆ F`` ensures ``(âˆ€á¶  x in F, P x) â†’ (âˆ€ x, P x â†’ Q x) â†’ âˆ€á¶  x in F, Q x``, and
* the condition ``U âˆˆ F â†’ V âˆˆ F â†’ U âˆ© V âˆˆ F`` ensures ``(âˆ€á¶  x in F, P x) â†’ (âˆ€á¶  x in F, Q x) â†’ âˆ€á¶  x in F, P x âˆ§ Q x``.
EXAMPLES: -/
-- QUOTE:
#check @eventually_of_forall
#check @eventually.mono
#check @eventually.and
-- QUOTE.

/- TEXT:
The second item, corresponding to ``eventually.mono``, supports nice ways
of using filters, especially when combined
with ``eventually.and``. The ``filter_upwards`` tactic allows us to combine them.
Compare:
EXAMPLES: -/
-- QUOTE:
example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n)
  (hR : âˆ€á¶  n in at_top, P n âˆ§ Q n â†’ R n) :
  âˆ€á¶  n in at_top, R n :=
begin
  apply (hP.and (hQ.and hR)).mono,
  rintros n âŸ¨h, h', h''âŸ©,
  exact h'' âŸ¨h, h'âŸ©
end

example (P Q R : â„• â†’ Prop) (hP : âˆ€á¶  n in at_top, P n) (hQ : âˆ€á¶  n in at_top, Q n)
  (hR : âˆ€á¶  n in at_top, P n âˆ§ Q n â†’ R n) :
  âˆ€á¶  n in at_top, R n :=
begin
  filter_upwards [hP, hQ, hR],
  intros n h h' h'',
  exact h'' âŸ¨h, h'âŸ©
end
-- QUOTE.

/- TEXT:
Readers who know about measure theory will note that the filter ``Î¼.ae`` of sets whose complement has measure zero
(aka "the set consisting of almost every point") is not very useful as the source or target of ``tendsto``, but it can be conveniently
used with ``eventually`` to say that a property holds for almost every point.

There is a dual version of ``âˆ€á¶  x in F, P x``, which is occasionally useful:
``âˆƒá¶  x in F, P x`` means
``{x | Â¬P x} âˆ‰ F``. For example, ``âˆƒá¶  n in at_top, P n`` means there are arbitrarily large ``n`` such that ``P n`` holds.
The ``âˆƒá¶ `` notation stands for ``filter.frequently``.

For a more sophisticated example, consider the following statement about a sequence
``u``, a set ``M``, and a value ``x``:

  If ``u`` converges to ``x`` and ``u n`` belongs to ``M`` for
  sufficiently large ``n`` then ``x`` is in the closure of ``M``.

This can be formalized as follows:

  ``tendsto u at_top (ğ“ x) â†’ (âˆ€á¶  n in at_top, u n âˆˆ M) â†’ x âˆˆ closure M``.

This is a special case of the theorem ``mem_closure_of_tendsto`` from the
topology library.
See if you can prove it using the quoted lemmas,
using the fact that ``cluster_pt x F`` means ``(ğ“ x âŠ“ F).ne_bot``.

EXAMPLES: -/
-- QUOTE:
#check mem_closure_iff_cluster_pt
#check le_principal_iff
#check ne_bot_of_le

example (u : â„• â†’ â„) (M : set â„) (x : â„)
  (hux : tendsto u at_top (ğ“ x)) (huM : âˆ€á¶  n in at_top, u n âˆˆ M) : x âˆˆ closure M :=
sorry
-- QUOTE.

-- SOLUTIONS:
example (u : â„• â†’ â„) (M : set â„) (x : â„)
  (hux : tendsto u at_top (ğ“ x)) (huM : âˆ€á¶  n in at_top, u n âˆˆ M) : x âˆˆ closure M :=
mem_closure_iff_cluster_pt.mpr (ne_bot_of_le $ le_inf hux $ le_principal_iff.mpr huM)
